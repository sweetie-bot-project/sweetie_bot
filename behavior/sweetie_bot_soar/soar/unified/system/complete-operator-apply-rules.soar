#
# APPLY RULES FOR COMPLETE OPERATORS
#

# copmletion operator with status
sp {apply*complete*remove-substate-process
	(state <s> ^opertor <op> ^beliefs <b> ^time.now <time>)
	(<op> ^type completion ^status <status> ^process <pred>)
	(<pred> ^name <name>)
-->
	(<pred> ^terminated-at <time> ^status <status>)
	(<b> ^process <pred> -)
	(<b> ^event <ev>)
	(<ev> ^name <name> ^reason terminated ^status <status> ^process <pred> ^initiated-at <time>)
}

#
# GENERAL WME MANIPULATIONS
#

# copy simple attribute or link o-supported indentifer to WME
sp {apply*complete*link-value
	(state <s> ^operator <op>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^parent <p> ^attribute <attr> ^link-value <value>)
-->
	(<p> ^<attr> <value>)
}

# copy i-supported structure with one level of attribution
sp {apply*complete*copy-value-one-level
	(state <s> ^operator <op>)
	(<op> ^type complete ^status <status> ^results.result <result>)
	(<result> ^status <status> ^parent <p> ^attribute <attr> ^copy-value-one-level <value>)
-->
	(<p> ^<attr> (copy-one-level <value>))
}

# remove attribute with specific value
sp {apply*complete*remove-value
	(state <s> ^operator <op>)
	(<op> ^type complete ^status <status> ^results.result <result>)
	(<result> ^status <status> ^parent <p> ^attribute <attr> ^remove-value {<> |__all__| <value>})
-->
	(<p> ^<attr> <value> -)
}

# remove all attributes
sp {apply*complete*remove-all-values
	(state <s> ^operator <op>)
	(<op> ^type complete ^status <status> ^results.result <result>)
	(<result> ^status <status> ^parent <p> ^attribute <attr> ^remove-value __all__)
	(<p> ^<attr> <value>)
-->
	(<p> ^<attr> <value> -)
}

# copy simple retval or link o-supported retavl indentifer to WME
sp {apply*complete*retval-link-value
	(state <s> ^operator <op>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^parent <p> ^retval-attribute <attr> ^retval-link <retval-name>)
	(<op> ^<retval-name> <value>)
-->
	(<p> ^<attr> <value>)
}

# copy retval structure with one level of attribution
sp {apply*complete*retval-copy-value-one-level
	(state <s> ^operator <op>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^parent <p> ^retval-attribute <attr> ^retval-copy-one-level <retval-name>)
	(<op> ^<retval-name> <value>)
-->
	(<p> ^<attr> (copy-one-level <value>))
}

#
# EVENTS HANDLING
#

# add event: create predicate WME, add `^initiated-at`
sp {apply*complete*add-event
	(state <s> ^operator <op> ^beliefs <b> ^time.now <time>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^add-event <ev>)
-->
	(<b> ^event <ev>)
	(<ev> ^initiated-at <time>)
}

# add event: add to history
sp {apply*complete*add-event*history
	(state <s> ^operator <op> ^events <ev-pool> ^time.now <time>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^add-event <ev>)
	(<ev> ^memorization-time <mem-time>)
-->
	(<ev-pool> ^event <ev>)
}

#
# PREDICATES HANDLING
#

# add predicate: create predicate WME, add `^initiated-at`, add to pending 
sp {apply*complete*add-predicate
	(state <s> ^operator <op> ^beliefs <b> ^beliefs-stable.pending <bs> ^time.now <time>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^add-predicate <pred> -^retval-attribute)
	(<pred> ^name <name>)
-->
	(<b> ^predicate <pred>)
	(<pred> ^initiated-at <time>)
	(<b> ^event <ev>)
	(<ev> ^name <name> ^reason initiated ^predicate <pred> ^initiated-at <time>)
	(<bs> ^predicate <pred>)
}

# add predicate: add to history
sp {apply*complete*add-predicate*history
	(state <s> ^operator <op> ^predicates <pred-pool>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^add-predicate <pred> -^retval-attribute)
	(<pred> ^memorization-time <mem-time>)
-->
	(<pred-pool> ^predicate <pred>)
}

# remove predicate
sp {apply*complete*remove-predicate
	(state <s> ^operator <op> ^beliefs <b> ^time.now <time>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^remove-predicate <pred>)
	(<pred> ^name <name>)
-->
	(<pred> ^terminated-at <time>)
	(<b> ^predicate <pred> -)
	(<b> ^event <ev>)
	(<ev> ^name <name> ^reason terminated ^predicate <pred> ^initiated-at <time>)
}

# add predicate with simple attribute or o-supported structure retval 
sp {apply*complete*add-predicate-retval-link
	(state <s> ^operator <op> ^beliefs <b> ^beliefs-stable.pending <bs> ^time.now <time>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^add-predicate <pred> ^retval-attribute <attr> ^retval-link <retval-name>)
	(<op> ^<retval-name> <value>)
	(<pred> ^name <name>)
-->
	(<b> ^predicate <pred>)
	(<pred> ^<attr> <value>)
	(<pred> ^initiated-at <time>)
	(<b> ^event <ev>)
	(<ev> ^name <name> ^reason initiated ^predicate <pred> ^initiated-at <time>)
	(<bs> ^predicate <pred>)
}


# PROCESS HANDLING
#

# add predicate: create predicate WME, add `^initiated-at`, add to pending 
sp {apply*complete*add-process
	(state <s> ^operator <op> ^beliefs <b> ^beliefs-stable.pending <bs> ^time.now <time>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^add-process <pred> -^retval-attribute)
	(<pred> ^name <name>)
-->
	(<b> ^process <pred>)
	(<pred> ^initiated-at <time>)
	(<b> ^event <ev>)
	(<ev> ^name <name> ^reason initiated ^process <pred> ^initiated-at <time>)
	(<bs> ^process <pred>)
}

# add process: add to history
sp {apply*complete*add-process*history
	(state <s> ^operator <op> ^processs <pred-pool>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^add-process <pred> -^retval-attribute)
	(<pred> ^memorization-time <mem-time>)
-->
	(<pred-pool> ^process <pred>)
}

# remove process
sp {apply*complete*remove-process
	(state <s> ^operator <op> ^beliefs <b> ^time.now <time>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^remove-process <pred>)
	(<pred> ^name <name>)
-->
	(<pred> ^terminated-at <time>)
	(<b> ^process <pred> -)
	(<b> ^event <ev>)
	(<ev> ^name <name> ^reason terminated ^process <pred> ^initiated-at <time>)
}

# add process with simple attribute or o-supported structure retval 
sp {apply*complete*add-process-retval-link
	(state <s> ^operator <op> ^beliefs <b> ^beliefs-stable.pending <bs> ^time.now <time>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^add-process <pred> ^retval-attribute <attr> ^retval-link <retval-name>)
	(<op> ^<retval-name> <value>)
	(<pred> ^name <name>)
-->
	(<b> ^process <pred>)
	(<pred> ^<attr> <value>)
	(<pred> ^initiated-at <time>)
	(<b> ^event <ev>)
	(<ev> ^name <name> ^reason initiated ^process <pred> ^initiated-at <time>)
	(<bs> ^process <pred>)
}


# DECISION HANDLING
#

# add predicate: create predicate WME, add `^initiated-at`, add to pending 
sp {apply*complete*add-decision
	(state <s> ^operator <op> ^beliefs <b> ^beliefs-stable.pending <bs> ^time.now <time>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^add-decision <pred> -^retval-attribute)
	(<pred> ^name <name>)
-->
	(<b> ^decision <pred>)
	(<pred> ^initiated-at <time>)
	(<b> ^event <ev>)
	(<ev> ^name <name> ^reason initiated ^decision <pred> ^initiated-at <time>)
	(<bs> ^decision <pred>)
}

# remove decision
sp {apply*complete*remove-decision
	(state <s> ^operator <op> ^beliefs <b> ^time.now <time>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^remove-decision <pred>)
	(<pred> ^name <name>)
-->
	(<pred> ^terminated-at <time>)
	(<b> ^decision <pred> -)
	(<b> ^event <ev>)
	(<ev> ^name <name> ^reason terminated ^decision <pred> ^initiated-at <time>)
}

# add decision with simple attribute or o-supported structure retval 
sp {apply*complete*add-decision-retval-link
	(state <s> ^operator <op> ^beliefs <b> ^beliefs-stable.pending <bs> ^time.now <time>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^add-decision <pred> ^retval-attribute <attr> ^retval-link <retval-name>)
	(<op> ^<retval-name> <value>)
	(<pred> ^name <name>)
-->
	(<b> ^decision <pred>)
	(<pred> ^<attr> <value>)
	(<pred> ^initiated-at <time>)
	(<b> ^event <ev>)
	(<ev> ^name <name> ^reason initiated ^decision <pred> ^initiated-at <time>)
	(<bs> ^decision <pred>)
}

#
# ADDITIONAL COMMANDS
#

# print message
sp {apply*complete*print
	(state <s> ^operator <op>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^print <value>)
-->
	(write <value> (crlf))
}

#
# CHANGE ATTRIBUTES SUPPORT
#
# i-supported results created in top state preserves i-support after being copied to beliefs
# so make them o-supported explicitly
#

sp {apply*complete*make-top-state-results-o-supported
	(state <s> ^operator <op>)
	(<op> ^type completion ^status <status> ^results.result <result>)
	(<result> ^status <status> ^{<<|add-predicate| |add-event| |add-process| |add-decision|>> <command>} <pred>)
	(state <ss> ^top-state 1 ^operator.result.<command> <pred>)
	(<pred> ^<attr> <value>)
-->
	(<pred> ^<attr> <value>)
}
