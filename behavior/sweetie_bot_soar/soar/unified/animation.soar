# ANIMATION SUBSTATE
#
# Start animation with given tags
#  
# DECISION OPERATOR
#
# (<op> ^name @name@ ^substate animation ^process-args <args>)
#   (<args> ^tag @tag1@ [@tag2])
#
# DESCRIPTION
#
# Selects animation tagged with provided tags and executes it. One or two tags must be supplied.
#
# PROCESS RESULT STATUSES
#
# Agument supplied process with flexbe arguments and use it flexbe substate for behavior ExecuteCompoundAction.
# Can produce corresponding results.
#
# SIDE EFFECTS
#
# None.
#

# ELABORATIONS


# SELECT ANIMATION TO EXECUTION

# propose animations: select animation with corresponding tag (one tag is provided)
sp {animation*propose*animation-one-tag
	(state <s> ^substate animation ^process-status pending ^process <proc> ^args <args> ^mem <mem>)
	(<args> ^tag <tag> -^tag {<> <tag>})
	(<mem> ^compound-action <anim>)
	(<anim> ^name <action_name> ^tag <tag>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name animation-start ^substate flexbe ^process-link <proc> ^additional-process-args <a>)
	(<a> ^behavior ExecuteCompoundAction ^action_name <action_name>)
}

# propose animations: select animation with corresponding tags (two tags are provided)
sp {animation*propose*animation-two-tags
	(state <s> ^substate animation ^process-status pending ^process <proc> ^args <args> ^mem <mem>)
	(<args> ^tag <tag1> ^tag {<> <tag1> <tag2>} -^tag {<> <tag1> <> <tag2>})
	(<mem> ^compound-action <anim>)
	(<anim> ^name <action_name> ^tag <tag1> ^tag <tag2>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name animation-start ^substate flexbe ^process-link <proc> ^additional-process-args <a>)
	(<a> ^behavior ExecuteCompoundAction ^action_name <action_name>)
}

# WAIT WHILE PROCESS IS ACTIVE

sp {animation*propose*wait
	(state <s> ^substate animation ^process-status active ^process <proc>)
	(<proc> ^action_name <action_name>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name animation-wait ^substate flexbe ^process-link <proc>)
}
