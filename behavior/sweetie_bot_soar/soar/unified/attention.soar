#
# ATTENTION MODULE
#
# DESCRIPTION
#
# Focus attention on one of visible object. State no change if not point to focus attention.
# Attention switch is based on distance, object time and apperance.
#
# DECISION OPERATOR
#
# (<op> ^name @name@ ^substate attention)
#
# RESULT STATUSES
#
# SIDE EFFECTS
#
# Output link: 
#
# * flexbe behavior
#
# Decision:
#
# * (^name attention-focus ^substate attention ^object <obj>) --- object is selected as attention focus.
#

# UTILITY ELABORATION

# produce evaluation frames: use type, distance-head and perceive-begin attributes for visible objects
sp {attention*elaborate*evaluation-frames
	(state <s> ^substate attention ^beliefs.object <obj>)
	(<obj> ^type <type> ^input-link <il>)
	(<il> ^visible now ^perceive-begin <p-time> ^distance-head <d-head>)
-->
	(<s> ^evaluation-frame <e>)
	(<e> ^object <obj> ^type <type> ^perceive-begin <p-time> ^distance-head <d-head>)
}

# parameters, utility map for different attributes values
sp {attention*elaborate*preferences-and-utility
	(state <s> ^substate attention)
-->
	(<s> ^focus-reconsider-time 10.0)
	(<s> ^attempt-to-focus-time 5.0)
	(<s> ^preference <p1> <p2> <p3>)
	(<p1> ^attribute type ^map <m1> ^utility utilityA ^default -100)
	(<m1> ^human 20 ^pony 10)	
	(<p2> ^attribute distance-head ^map <m2> ^utility utilityB ^default 0)
	(<m2> ^close 200 ^near 50)	
	(<p3> ^attribute perceive-begin ^map <m3> ^utility utilityC ^default 0)
	(<m3> ^now 100 ^recently 50)	
}

# ELABORATIONS 

# attach related decisions to state
sp {attention*evaluate*recover-decision
	(state <s> ^substate attention ^beliefs.decision <d>)
	(<d> ^name attention-focus ^substate attention)
-->
	(<s> ^decision <d>)
}

# calculate reconsider decision threshold 
sp {attention*elaborate*reconsider-decision-utility-threshold
	(state <s> ^substate attention ^decision <d> ^evaluation-frame <eval>)
	(<d> ^object <obj>)
	(<eval> ^object <obj> ^utility <utility>)
-->
	(<s> ^decision-reconsider-utility-threshold (+ <utility> 100))
}

# DECISION OPERATORS

# decide what object we should look at: use numeric preferences 
sp {attention*propose*select-attention-focus
	(state <s> ^substate attention ^beliefs <b> ^evaluation-frame <en> ^focus-reconsider-time <timeout>)
	(<en> ^object <obj> ^utility <utility>)
	(<b> -^decision.name attention-focus)
-->
	(<s> ^operator <op> +, = <utility>)
   	(<op> ^name select-attention-focus ^type automatic ^object <obj> ^drive <d> ^add-decision <ds>) 
	(<ds> ^name attention-focus ^substate attention ^object <obj> ^deadline-time <timeout>)
}

# reconside active decision if its utility is greater then threshold
sp {attention*propose*switch-atttention-focus
	(state <s> ^substate attention ^decision <ds> ^evaluation-frame <eval-new> ^decision-reconsider-utility-threshold <threshold> ^focus-reconsider-time <delay>)
	(<eval-new> ^utility {>= <threshold> <utility>} ^object <obj>)
-->
	(<s> ^operator <op> +, = <utility>)
   	(<op> ^name switch-attention-focus ^type automatic ^add-decision <new-ds> ^remove-decision <ds>) 
	(<new-ds> ^name attention-focus ^substate attention ^object <obj> ^deadline-time <delay>)
}

# look at the object. If action failed then reconsider desicion.
sp {attention*propose*look-at
	(state <s> ^substate attention ^beliefs.decision <ds> ^attempt-to-focus-time <timeout>)
	(<ds> ^name attention-focus ^object <obj> ^deadline-time <delay>)
-->
	(<s> ^operator <op> +) 
	(<op> ^name look-at-attention-focus ^substate look-at ^object <obj> ^delay 2.0 ^timeout <timeout> ^result <res>)
	(<res> ^status failed ^remove-decision <ds>) 
}

# PREFERENCES

# prefer switch attention over look at
sp {attention*prefer*switch
	(state <s> ^substate attention ^operator <op1> + ^operator {<> <op1> <op2>} +)
	(<op1> ^name switch-attention-focus)
	(<op2> ^name look-at-attention-focus)
-->
	(<s> ^operator <op1> > <op2>)
}
