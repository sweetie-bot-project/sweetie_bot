#
# ATTENTION MODULE
#
# DESCRIPTION
#
# Focus attention on one of visible object. State no change if not point to focus attention.
# Attention switch is based on distance, object time and apperance.
#
# DECISION OPERATOR
#
# (<op> ^name @name@ ^substate attention)
#
# RESULT STATUSES
#
# SIDE EFFECTS
#
# Output link: 
#
# * flexbe behavior
#
# Decision:
#
# * (^name attention-focus ^substate attention ^object <obj>) --- object is selected as attention focus.
#

# UTILITY ELABORATION

# produce evaluation frames: use type, distance-head and perceive-begin attributes for visible objects
sp {attention*elaborate*evaluation-frames
	(state <s> ^substate attention ^beliefs.object <obj>)
	(<obj> ^type <type> ^input-link <il>)
	(<il> ^visible now ^perceive-begin <p-time> ^distance-head <d-head>)
-->
	(<s> ^evaluation-frame <e>)
	(<e> ^object <obj> ^type <type> ^perceive-begin <p-time> ^distance-head <d-head>)
}

# parameters, utility map for different attributes values
sp {attention*elaborate*preferences-and-utility
	(state <s> ^substate attention)
-->
	(<s> ^focus-reconsider-time 10.0)
	(<s> ^attempt-to-focus-time 5.0)
	(<s> ^preference <p1> <p2> <p3>)
	(<p1> ^attribute type ^map <m1> ^utility utilityA ^default -100)
	(<m1> ^human 20 ^pony 10)	
	(<p2> ^attribute distance-head ^map <m2> ^utility utilityB ^default 0)
	(<m2> ^close 200 ^near 50)	
	(<p3> ^attribute perceive-begin ^map <m3> ^utility utilityC ^default 0)
	(<m3> ^now 100 ^recently 50)	
}

# ELABORATIONS 

# attach related processe to state
sp {attention*evaluate*recover-focusing-attention-process
	(state <s> ^substate attention ^beliefs.process <proc> ^process <sproc>)
	(<proc> ^name focusing-attention ^substate attention ^subprocess-of <sproc>)
-->
	(<s> ^focusing-attention <proc>)
}

# calculate reconsider decision threshold 
sp {attention*elaborate*reconsider-decision-utility-threshold
	(state <s> ^substate attention ^focusing-attention <proc> ^evaluation-frame <eval>)
	(<proc> ^object <obj>)
	(<eval> ^object <obj> ^utility <utility>)
-->
	(<s> ^decision-reconsider-utility-threshold (+ <utility> 100))
}

# DECISION OPERATORS

# look at the object
sp {attention*propose*look-at-new-atention-focus
	(state <s> ^substate attention -^focusing-attention ^evaluation-frame <en> ^superstate.operator.process <sproc>)
	(<en> ^object <obj> ^utility <utility>)
	(<s> ^focus-reconsider-time <reconsider-time> ^attempt-to-focus-time <focus-timeout>)
-->
	(<s> ^operator <op> +, = <utility>)
	(<op> ^name look-at-new-attention-focus ^substate look-at ^object <obj> ^delay 2.0 ^timeout <focus-timeout> ^process <proc>)
	(<proc> ^name focusing-attention ^object <obj> ^deadline-time <reconsider-time> ^start-process <sproc>) 
}

# continue looking at the object
sp {attention*propose*look-at-attention-focus
	(state <s> ^substate attention ^focusing-attention <proc> ^attempt-to-focus-time <focus-timeout>)
	(<proc> ^object <obj>)
-->
	(<s> ^operator <op> +) 
	(<op> ^name look-at-attention-focus ^substate look-at ^object <obj> ^delay 2.0 ^timeout <focus-timeout> ^process <proc>)
}

# conside new attention focus if its utility is greater then threshold
sp {attention*propose*switch-atention-focus
	(state <s> ^substate attention ^focusing-attention <proc-old> ^evaluation-frame <eval-new> ^decision-reconsider-utility-threshold <threshold>)
	(<eval-new> ^utility {>= <threshold> <utility>} ^object <obj>)
	(<s> ^focus-reconsider-time <reconsider-time> ^attempt-to-focus-time <focus-timeout>)
-->
	(<s> ^operator <op> +, = <utility>)
	(<op> ^name look-at-switch-attention-focus ^substate look-at ^object <obj> ^delay 2.0 ^timeout <focus-timeout> ^process <proc-new>)
	(<proc-new> ^name focusing-attention ^object <obj> ^deadline-time <reconsider-time> ^start-process <sproc>) 
}

# PREFERENCES

# prefer switch attention over look at
sp {attention*prefer*switch
	(state <s> ^substate attention ^operator <op1> + ^operator {<> <op1> <op2>} +)
	(<op1> ^name look-at-switch-attention-focus)
	(<op2> ^name look-at-attention-focus)
-->
	(<s> ^operator <op1> > <op2>)
}
