#
# FLEXBE SUBSTATE
#  
# Start text action substate.
#
# DESCRIPTION
#
# It waits current text action if it presents. Then execute text action with given parameters.
# with name <behavior-name> and given parameters. 
#
# DECISION OPERATOR
#
# (<op> ^name <name> ^substate text-action ^process-args <args>)
#   (<args> ^command <text-command> ^type <text-command-type>)
#
# PROCESS RESULT STATUSES
#
#	* FlexBe behavior outcomes (standart convention): 
#		* succeed --- text action succeed
#		* failed --- text action failed
#
# SIDE EFFECTS (not implemented)
#
#	* event(^name error ^type failed ^subsystem text-action) --- added on "failed" result
#

# ELABORATIONS

# add process if any action is performed
sp {text-action*elaborate*add-porcess
	(state <s> ^substate text-action ^process-status pending ^operator <op> + ^process <proc>)
	(<op> ^name {<<|start-text-action| |abort-text-action|>>})
-->
	(<op> ^type automatic ^add-process <proc>)
}

# DECISIONS

# abort active text action
sp {text-action*propose*abort-text-action
	(state <s> ^substate text-action ^process <proc> ^io.output-link.text-action <cmd>)
	(<cmd> ^process {<> <proc>} -^abort)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name abort-text-action ^type automatic ^parent <cmd> ^attribute abort ^link-value hard)
}

# propose start text-action behavior if no text-action is running
sp {text-action*propose*start-text-action
	(state <s> ^substate text-action  ^io.output-link <ol> ^args <a>)
	(<ol> -^text-action)
	(<a> ^command <cmd> ^type <type>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name start-text-action ^action-command <cmd> ^action-type <type>)
}

# create output link command
sp {text-action*apply*start-text-action
	(state <s> ^substate text-action ^operator <op> ^io.output-link <ol> ^process <proc>)
	(<op> ^name start-text-action ^action-command <text> ^action-type <type>)
-->
	(<ol> ^text-action <cmd>)
	(<cmd> ^command <text> ^type <type>)
	(<cmd> ^process <proc>)
}

# wait text-action behavior 
sp {text-action*propose*wait 
	(state <s> ^substate text-action)
-->
	(<s> ^operator <op> +, <)
	(<op> ^name text-action-wait-cmd ^substate wait)
}

# COGNITION

# finish process with coresponding status and remove output command 
sp {text-action*propose*complete
	(state <s> ^bottom-state 1 ^io.output-link.text-action <cmd>)
	(<cmd> ^status <status> ^process <proc>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name text-action-complete ^substate text-action ^type automatic cognition)
	(<op> ^finish-status <status> ^finish-process <proc>)
	(<op> ^remove-output-command text-action ^output-command-args <cmd>)
}

