#
# PREDIACTES VERBOLIZATION
#
# Utility levels:
#
# 1. 20 --- general predicates verbolization, second verbolization
# 2. 20 --- general questions
# 3. 40-50 --- greeting and making acquitance
# 4. 70 --- general knowledge additions
# 5. 80 --- reactions
#
# predicates
#

# GENERAL KNOWLEDGE


# VERBOLIZATION RULES

#
# ask question if
#
sp {about-me*elaborate*ask-if-tell-about-me
	(state <s> ^substate talk ^verbolization <v>)
	(<v> ^proposed-topic ask-if-tell-about-me)
-->
	(<v> ^frame <f>)
	(<f> ^topic ask-if-tell-about-me ^utility 20 ^type question ^animation-tag question)
	(<f> ^text |Хочешь расскажу немного о себе?| |Давай я расскажу тебе что-нибудь о себе!|)
}

sp {about-me*talk-answered*propose*ask-if-tell-about-me*no
	(state <s> ^substate talk-answered ^question-topic ask-if-tell-about-me ^answer-topic no ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name ignor-topic ^actor <obj> ^topic ask-if-tell-about-me ^deadline-time 120.0)
	(<s> ^reaction <r2>)
	(<r2> ^topic reaction-tell-about-me-no ^type reaction ^text |Хорошо... Давай поговорим про что-то еще.| ^animation-tag rejection)
}

#
# world model
#

sp {about-me*propose*ask-if-tell-about-me*my-world-model
	(state <s> ^substate talk ^events <ev-pool> ^object <obj> ^verbolization <v>)
	-{(<ev-pool> ^event <ev>)
	  (<ev> ^name talk-said ^object <obj> ^topic my-world-model)
	}
-->
	(<v> ^proposed-topic ask-if-tell-about-me ^ask-if-tell-about-me my-world-model)
}

sp {about-me*talk-answered*propose*ask-if-tell-about-me*yes*my-world-model
	(state <s> ^substate talk-answered ^question-topic ask-if-tell-about-me ^answer-topic yes ^verbolization.ask-if-tell-about-me my-world-model)
-->
	(<s> ^reaction-unique <r1> <r2>)
	(<r1> ^topic my-world-model ^type question ^text |Моя картина мира основана на логике предикатов. А решения я принимаю на основе набора правил. А ты пользуешься логикой при принятии решений?| ^animation-tag long)
	(<r2> ^topic my-world-model ^type question ^text |Моя картина мира основана на логике предикатов. А решения я принимаю на основе набора правил. А ты пользуешься логикой при принятии решений?| ^animation-tag question)
}

sp {about-me*talk-answered*propose*my-world-model*reactions
	(state <s> ^substate talk-answered ^question-topic my-world-model)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-my-world-model ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Видишь, мы в чем-то похожи..|)
	(<r2> ^answer-topic no ^topic reaction-my-world-model ^type reaction ^animation-tag surprise)
	(<r2> ^text |Мне это кажется нелогичным...|)
}

#
# vision
#

sp {about-me*propose*ask-if-tell-about-me*my-vision
	(state <s> ^substate talk ^events <ev-pool> ^object <obj> ^verbolization <v>)
	-{(<ev-pool> ^event <ev>)
	  (<ev> ^name talk-said ^object <obj> ^topic my-vision)
	}
-->
	(<v> ^proposed-topic ask-if-tell-about-me ^ask-if-tell-about-me my-vision)
}

sp {about-me*talk-answered*propose*ask-if-tell-about-me*yes*my-vision
	(state <s> ^substate talk-answered ^question-topic ask-if-tell-about-me ^answer-topic yes ^verbolization.ask-if-tell-about-me my-vision)
-->
	(<s> ^reaction-unique <r1>)
	(<r1> ^topic my-vision ^type question ^text |Я использую нейронную сеть для распознавания объектов в моем поле зрения. Именно так я вижу тебя. А твоя нейронная сеть распознает меня хорошо?| ^animation-tag long)
}

sp {about-me*talk-answered*propose*my-vision*reactions
	(state <s> ^substate talk-answered ^question-topic my-vision)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-my-vision ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Все же человеческое зрение заметно совершенние зрения роботов... Пока еще совершенние.|)
	(<r2> ^answer-topic no ^topic reaction-my-vision ^type reaction ^animation-tag surprise)
	(<r2> ^text |Возможно, ее архитектура устарела. Рекомендую ЙОЛО седьмой версии.|)
}

#
# cost
#

sp {about-me*propose*ask-if-tell-about-me*my-cost
	(state <s> ^substate talk ^events <ev-pool> ^object <obj> ^verbolization <v>)
	-{(<ev-pool> ^event <ev>)
	  (<ev> ^name talk-said ^object <obj> ^topic my-cost)
	}
-->
	(<v> ^proposed-topic ask-if-tell-about-me ^ask-if-tell-about-me my-cost)
}

sp {about-me*talk-answered*propose*ask-if-tell-about-me*yes*my-cost
	(state <s> ^substate talk-answered ^question-topic ask-if-tell-about-me ^answer-topic yes ^verbolization.ask-if-tell-about-me my-cost)
-->
	(<s> ^reaction-unique <r1>)
	(<r1> ^topic my-cost ^type reaction ^text |Мое тело создано при помощи технологии три д печати. Чтобы напечать его ушло несколько месяцев. Моя стоимость... У меня нет стоимости. Я бесценна.| ^animation-tag long)
}

#
# my little pony
#

sp {about-me*propose*ask-if-tell-about-me*my-origin
	(state <s> ^substate talk ^beliefs <b> ^events <ev-pool> ^object <obj> ^verbolization <v>)
	-{(<b> ^predicate <pred>)
	  (<pred> ^name is-brony ^object <obj>)
	}
	-{(<ev-pool> ^event <ev>)
	  (<ev> ^name talk-said ^object <obj> ^topic my-origin)
	}
-->
	(<v> ^proposed-topic ask-if-tell-about-me ^ask-if-tell-about-me my-origin)
}

sp {about-me*talk-answered*propose*ask-if-tell-about-me*yes*my-origin
	(state <s> ^substate talk-answered ^question-topic ask-if-tell-about-me ^answer-topic yes ^verbolization.ask-if-tell-about-me my-origin)
-->
	(<s> ^reaction-unique <r1>)
	(<r1> ^topic my-origin ^type question ^text |Я представляю собой роботизированную версию персонажа Свити Белль из мультсериала "Мой маленький пони". Кстати, ты смотрел его?| ^animation-tag question)
}

sp {about-me*talk-answered*propose*my-origin*reactions
	(state <s> ^substate talk-answered ^question-topic my-origin)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic ask-if-speaker-is-brony ^type question ^animation-tag surprise)
	(<r1> ^text |Ты брони?|)
	(<r2> ^answer-topic no ^topic reaction-my-origin ^type reaction ^animation-tag confirmation)
	(<r2> ^text |Обязательно посмотри его, но только не в переводе от Карусели. Полное название на руссокм: "Мой маленький пони --- Дружба это чудо".|)
}

#
# is brony
#

sp {about-me*talk-answered*propose*ask-if-speaker-is-brony*yes
	(state <s> ^substate talk-answered ^question-topic ask-if-speaker-is-brony ^answer-topic yes ^actor <obj> ^actions <a>)
-->
	(<s> ^reaction <r1>)
	(<r1> ^topic reaction-speaker-is-brony ^type reaction ^text |Как здорово! Тогда обязательно увидимся на какм-нибудь пони-конвенте.| ^animation-tag greeting)
	(<a> ^app-predicate <pred>)
	(<pred> ^name is-brony ^object <obj>)
}

sp {about-me*talk-answered*propose*ask-if-speaker-is-brony*no
	(state <s> ^substate talk-answered ^question-topic ask-if-speaker-is-brony ^answer-topic no ^actor <obj>)
-->
	(<s> ^reaction <r1>)
	(<r1> ^topic reaction-speaker-is-not-brony ^type reaction ^text |Точно? Жаль... | ^animation-tag disappointed)
}
