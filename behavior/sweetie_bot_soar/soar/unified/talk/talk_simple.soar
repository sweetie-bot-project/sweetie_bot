	#
# TALKING
#
# 1. Use one of start phrases to begin dialog.
# 2. look at speaker until speech event
# 2. use llm to transcribe it
# 3. verbilize result
#
#
#  talk-event
#    talk-said
#       question
#       statement
#    talk-heard
#       unknown-class
#       known-class
#       answer
#          irrelevant
#    talk-ignored
#    talk-pause
#    talk-illegible
#       

#
# COGNITION RULES
#

# expect person to leave if goodbye is said
sp {talk-llm*propose*expecting-to-leave
	(state <s> ^bottom-state 1 ^beliefs <b>)
	(<b> ^event <ev>)
	(<ev> ^name talk-said ^topic goodbye ^object <obj> -^processed expecting-to-leave)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name expecting-to-leave ^type cognition automatic ^add-predicate <pred-new> ^mark-processed <ev>)
	(<pred-new> ^name expecting-to-leave ^object <obj> ^deadline-time 60.0)
}

#
# ELABORATIONS 
#

# RECENT TALK EVENTS

# create basic agumentation for talk substate
sp {talk-llm*elaborate*substate
	(state <s> ^substate talk-llm)
-->
	(<s> ^recent-events <ev>)	
}

# relevant talk-* events
sp {talk-llm*elaborate*recent-talk-events
	(state <s> ^substate talk-llm ^events.event <ev> ^time <clock> ^recent-events <ev-pool>)
	(<ev> ^name {<<|talk-said| |talk-heard| |talk-ignored| |talk-no-answer| |talk-illegible|>> <ev-name>} ^initiated-at <time>)
	-(<clock> ^recently {> <time>})
-->
	(<ev-pool> ^event <ev>)
}

# the most recent event
sp {talk-llm*elaborate-the-most-recent-talk-event
	(state <s> ^substate talk-llm ^recent-events <ev-pool>)
	(<ev-pool> ^event <ev>)
	(<ev> ^initiated-at <time>)
	-{(<ev-pool> ^event {<> <ev> <ev-other>})
	  (<ev-other> ^initiated-at {> <time>})
	}
-->
	(<s> ^most-recent-event <ev>)
}

# LANGUAGE

# inspect talk events for ^lang attrib and use last one
sp {talk-llm*elborate*lang-from-last-event
	(state <s> ^substate talk-llm ^recent-events <ev-pool>)
	(<ev-pool> ^event <ev1>)
	(<ev1> ^lang <lang> ^initiated-at <t1>)
	-{(<ev-pool> ^event <ev2>)
	  (<ev2> ^lang ^initiated-at {> <t1>})
	}
-->
	(<s> ^lang <lang> ^lang-source event)
}

# default language is always english
sp {talk-llm*elborate*default-lang
	(state <s> ^substate talk-llm -^lang-source event)
-->
	(<s> ^lang ru ^lang-source default)
}

# INTERLOCUTOR DERIVATION

# interlocutor: we talked with him, but do not expect him leaving or if he ignors me
sp {talk-llm*elborate*interlocutor
	(state <s> ^substate talk-llm ^recent-events.event <ev> ^beliefs <b>)
	(<ev> ^object <obj>)
	-{(<b> ^predicate <pred>)
	  (<pred> ^name expecting-to-leave ^object <obj>)
	}
	-{(<b> ^predicate <pred>)
	  (<pred> ^name ignors-me ^actor <obj>)
	}
-->
   (<s> ^interlocutor <obj>)
}

sp {talk-llm*elborate*visible-interlocutor
	(state <s> ^substate talk-llm ^interlocutor <obj>)
	(<obj> ^input-link.visible now)
-->
   (<s> ^visible-interlocutor <obj>)
}

# PAUSE
	
sp {talk-llm*elaborate*pause-after-event
	(state <s> ^substate talk-llm ^most-recent-event.initiated-at <t> ^time <time>)
	-(<time> ^just {< <t>})
-->
	(<s> ^pause talk)
}

sp {talk-llm*elaborate*pause-at-start
	(state <s> ^substate talk-llm -^most-recent-event)
-->
	(<s> ^pause start)
}

# SPEECH DETECTION

sp {talk-llm*elaborate*hearing-speech
	(state <s> ^substate talk-llm ^io.input-link.sound <snd>)
	(<snd> ^{<<|speech| |sound|>> <type>} 1)
-->
	(<s> ^hearing <type>)
}

#
# DECISION RULES
#


#
# contine current action
#

# detect verbolization subprocess
sp {talk-llm*elaborate*answering-subporcess
	(state <s> ^substate talk-llm ^{<<|llm-answering-on-event| |llm-answering-on-pause| |rule-asking| |rule-answering-on-event|>>} <proc>) 
-->
	(<s> ^talking me ^talking-process <proc>)
}

# continue verbolization process 
sp {talk-llm*propose*answering-continue
	(state <s> ^substate talk-llm ^talking-process <proc>)
	(<proc> ^name <name> ^substate <substate>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name <name> ^substate <substate> ^process-link <proc>)
}

# other interlocutor is talking
sp {talk-llm*elaborate*talking-other
	(state <s> ^substate talk-llm ^hearing speech)
-->
	(<s> ^talking other)
}

#
# answer on the last event
#
sp {talk-llm*propose*general-answer
	(state <s> ^substate talk-llm -^talking ^most-recent-event <ev> ^lang <lang> ^visible-interlocutor <obj>)
	(<ev> ^name {<<|talk-heard| |talk-ignored| |talk-no-answer| |talk-illegible|>>} ^object <obj> )
-->
 	(<s> ^operator <op> +, =)
	(<op> ^name llm-answering-on-event ^substate verbolize-llm ^process-args <args>)
	(<args> ^object <obj> ^lang <lang>)
}

#
# answer on long pause
#
sp {talk-llm*propose*answer-delay
	(state <s> ^substate talk-llm -^talking ^pause talk ^visible-interlocutor <obj>  ^lang <lang>)
-->
 	(<s> ^operator <op> +, =)
	(<op> ^name llm-answering-on-pause ^substate verbolize-llm ^process-args <args>)
	(<args> ^object <obj> ^lang <lang>)
}


# add recent talk events
sp {talk-llm*elaborate*verbolize-llm-related-events
	(state <s> ^substate talk-llm ^operator <op> + ^recent-events.event <ev>)
	(<op> ^substate verbolize-llm ^process-args <args>)
-->
	(<args> ^event <ev>)
}

# add predicates related to interlocutors
sp {talk-llm*elaborate*verbolize-llm-related-predicates-interlocutor
	(state <s> ^substate talk-llm ^operator <op> +  ^interlocutor <obj> ^beliefs.predicate <pred>)
	(<op> ^substate verbolize-llm ^process-args <args>)
	(<pred> ^{<<|object| |actor|>>} <obj> ^text)
-->
	(<args> ^predicate <pred>)
}

# add predicates related to target person (first phrase)
sp {talk-llm*elaborate*verbolize-llm-related-predicates-target
	(state <s> ^substate talk-llm ^operator <op> +  ^beliefs.predicate <pred>)
	(<op> ^substate verbolize-llm ^process-args <args>)
	(<args> ^object <obj>)
	(<pred> ^{<<|object| |actor|>>} <obj> ^text)
-->
	(<args> ^predicate <pred>)
}

#
# DETERMINISTTIC REACTIONS
#

# prefer deterministic reactions
sp {talk-llm*prefer*specific-over-general
	(state <s> ^substate talk-llm ^operator <op1> + ^operator <op2> +)
	(<op1> ^name rule-answering-on-event)
	(<op2> ^name {<<|llm-answering-on-event| |llm-answering-on-pause|>>})
-->
	(<s> ^operator <op1> > <op2>)
}

# GREETING

# Perform greeting if human robot looking at is not greeted.
sp {talk-llm*propose*greeting
	(state <s> ^substate talk-llm -^talking ^pause ^lang <lang> ^beliefs.object <obj>)
	(<obj> ^type human -^is-greeted ^being-looking-at)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name rule-asking ^substate verbolize ^process-args <proc>)
	(<proc> ^object <obj> ^topic greet ^result <res-succeed>)
	(<proc> ^text |Hello! I am Sweetie Bot! Speak while the button is pressed.| ^animation-tag greeting ^lang <lang> ^emotion neutral ^type statement)
	(<res-succeed> ^sataus succeed ^add-predicate <pred>)
	(<pred> ^name greeted ^object <obj>)
	(<res-succeed> ^print (concat |SPECIFIC: GREETING| (crlf)))
}

# augugment object with greeted attribute
sp {predicate*elaborate*greeted-object
	(state <s> ^top-state 1 ^beliefs.predicate <pred>)
	(<pred> ^name greeted ^object <obj>)
-->
	(<obj> ^is-greeted <pred>)
}

# augugment object with being looking-at
sp {predicate*elaborate*being-looking-at
	(state <s> ^top-state 1 ^beliefs.predicate <pred>)
	(<pred> ^name looking-at ^object <obj>)
-->
	(<obj> ^being-looking-at <pred>)
}

# terminate greeted predicate if human is not visible for enough time
sp {predicate*propose*greeted-remove
	(state <s> ^bottom-state 1 ^beliefs.predicate <pred>)
	(<pred> ^name greeted ^object <obj>)
	(<obj> ^input-link.perceive-end recently)
-->
	(<s> ^operator <op> +, =)
	(<op> ^type cognition automatic ^remove-predicate <pred>)
}


# STANDATRD REACTIONS

# say goodbye and end dialog if speaker has said goodbye
sp {talk-llm*propose*goodbye
	(state <s> ^substate talk-llm -^talking ^most-recent-event <ev> ^lang <lang>)
	(<ev> ^name talk-heard ^token goodbye ^object <obj>)
-->
 	(<s> ^operator <op> +, =)
	(<op> ^name rule-answering-on-event ^substate verbolize ^process-args <args>)
	(<args> ^object <obj> ^topic goodbye ^text |Goodbye!| ^animation-tag goodbye ^lang <lang> ^emotion neutral ^type statement)
	(<args> ^result <res>)
	(<res> ^status succeed ^print (concat |SPECIFIC: GOODBYE| (crlf)))
}

# if LLM is broken end dialog
sp {talk-llm*propose*failsafe-answer
	(state <s> ^substate talk-llm -^talking ^beliefs.event <ev> ^lang <lang> ^object <obj> ^process <proc>)
	(<ev> ^name {<<|llm-answering-on-pause| |llm-answering-on-event|>>} ^reason terminated ^status failed ^process.object <obj>)
-->
 	(<s> ^operator <op> +, =)
	(<op> ^name rule-answering-on-event ^substate verbolize ^process-args <args>)
	(<args> ^object <obj> ^topic goodbye ^text |Something bad happened with my world model... You are annoying me! Goodbye.!| ^animation-tag goodbye ^lang <lang> ^emotion angry ^type statement)
	(<args> ^result <res>)
	(<res> ^status succeed ^finish-status succeed ^finish-process <proc> ^print (concat |SPECIFIC: GOODBYE TO CLEAN BRAIN.| (crlf)))
}

# ignorance reaction
sp {talk-llm*propose*ignor-me-answer
	(state <s> ^substate talk-llm -^talking ^beliefs.predicate <pred> ^lang <lang> ^visible-interlocutor <obj> ^time.just <time> ^recent-events <ev-pool>)
	(<pred> ^name ignors-me ^actor <obj> ^initiated-at {> <time>})
	-{(<ev-pool> ^event <ev>)
	  (<ev> ^name talk-said ^object <obj> ^topic ignors-me)
    }
-->
 	(<s> ^operator <op> +, =)
	(<op> ^name rule-answering-on-event ^substate verbolize ^process-args <args>)
	(<args> ^object <obj> ^topic goodbye ignors-me ^text |You are ignoring me! Goodbye!| ^animation-tag angry ^lang <lang> ^emotion angry ^type statement)
	(<args> ^result <res>)
	(<res> ^status succeed ^print (concat |SPECIFIC: GOODBYE ON IGNORANCE.| (crlf)))
}

# missing speaker reaction
sp {talk-llm*propose*say-missing-answer
	(state <s> ^substate talk-llm -^talking ^beliefs.predicate <pred> ^lang <lang>)
	(<pred> ^name is-missing ^object <obj> ^initiated-at {> <time>})
-->
 	(<s> ^operator <op> +, =)
	(<op> ^name rule-answering-on-event ^substate verbolize ^process-args <args>)
	(<args> ^object <obj> ^topic goodbye ^skip look-at ^text |Interlocutor is missing.| ^animation-tag sad ^lang <lang> ^emotion sad ^type statement)
	(<args> ^result <res>)
	(<res> ^status succeed ^print (concat |SPECIFIC: MISSING.| (crlf)))
}

# EXTRA REACTIONS

# brohoof
sp {talk-llm*propose*brohoof
	(state <s> ^substate talk-llm  -^talking ^most-recent-event <ev> ^lang <lang>)
	(<ev> ^name talk-heard ^token hoof give ^object <obj>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name rule-answering-on-event ^substate verbolize ^process-args <args>)
	(<args> ^object <obj> ^topic greeting ^text |Good!| ^animation-tag brohoof ^lang <lang> ^emotion happy ^type statement)
	(<args> ^result <res>)
	(<res> ^status succeed ^print (concat |SPECIFIC: BROHOOF.| (crlf)))
}

# dance
sp {talk-llm*propose*dance
	(state <s> ^substate talk-llm -^talking ^most-recent-event <ev> ^lang <lang> ^process <proc>)
	(<ev> ^name talk-heard ^token dance please ^object <obj>)
-->
 	(<s> ^operator <op> +, =)
	(<op> ^name rule-answering-on-event ^substate verbolize ^process-args <args>)
	(<args> ^object <obj> ^topic greeting ^text |I am dancing!.. I am dancing!.. I am dancing!.. I am dancing!..| ^animation-tag dance ^lang <lang> ^emotion happy ^type statement)
	(<args> ^result <res>)
	(<res> ^status succeed ^print (concat |SPECIFIC: DANCE.| (crlf)))
}


#
# look-at
#

# looking at sound source
sp {talk-llm*propose*looking-at-speech
	(state <s> ^substate talk-llm ^hearing speech ^beliefs.object <obj> )
	(<obj> ^type speech ^label speech)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name looking-at-speech ^substate look-at ^process-args <a>)
	(<a> ^object <obj>)
}

# looking at speaker as the worst choice
sp {talk-llm*propose*looking-at-human
	(state <s> ^substate talk-llm ^beliefs.object <obj>)
	(<obj> ^type human ^input-link.visible now)
-->
	(<s> ^operator <op> +, <, =)
	(<op> ^name looking-at-human ^substate look-at ^object <obj> ^process-args <a>)
	(<a> ^object <obj> ^deadline-time 5.0)
}

sp {talk-llm*prefer*look-at-interlocutor
	(state <s> ^substate talk-llm  ^operator <op1> + ^operator {<> <op1> <op2>} + ^interlocutor <obj1> -^interlocutor <obj2>)
	(<op1> ^name looking-at-human ^object <obj1>)
	(<op2> ^name looking-at-human ^object <obj2>)
-->
	(<s> ^operator <op1> > <op2>)
}

sp {talk-llm*prefer*look-at-last-interlocutor
	(state <s> ^substate talk-llm  ^operator <op1> + ^operator {<> <op1> <op2>} + ^interlocutor <obj1> ^interlocutor <obj2> ^most-recent-event.object <obj1>)
	(<op1> ^name looking-at-human ^object <obj1>)
	(<op2> ^name looking-at-human ^object <obj2>)
-->
	(<s> ^operator <op1> > <op2>)
}

#
# finish dialog
#

# look around for any possible speakers
sp {talk-llm*propose*searching-anyone
	(state <s> ^substate talk-llm ^beliefs <b> ^lang <lang>)
    -{(<b> ^object <obj>)
      (<obj> ^type human ^input-link.visible now)
     }
-->
	(<s> ^operator <op> +, <, =)
	(<op> ^name searching-anyone ^substate flexbe ^process-args <a> ^interruptable hard)
	(<a> ^behavior ExecuteJointTrajectoryAndSay ^joint_trajectory |head_look_around| ^text |Where did everyone go?| ^lang <lang>)
}

# abort immediatelly if any human is found
sp {talk-llm*propose*seaching-anyone-abort
	(state <s> ^substate talk-llm ^searching-anyone <proc> ^beliefs.object <obj>  ^io.output-link.flexbe <cmd>)
	(<obj> ^type human ^input-link.visible now)
	(<cmd> -^status -^abort ^process <proc>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name searching-anyone-abort ^type automatic cognition)
	(<op> ^parent <cmd> ^attribute abort ^link-value hard)
}

# finish talk if no one is found
sp {talk-llm*propose*searching-anyone-have-failed-finish-talk
	(state <s> ^substate talk-llm ^beliefs.event <ev> ^process <proc>)
	(<ev> ^name searching-anyone ^reason terminated ^status succeed ^process.subprocess-of <proc>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name finish-talk ^type cognition automatic ^finish-status succeed ^finish-process <proc>)
}

#
# missing speaker
#

# speaker is considered missing if he did not answered question (?)
sp {talk-llm*propose*searching-missing-speaker
	(state <s> ^substate talk-llm -^searching-missing-speaker ^beliefs.predicate <pred> ^lang <lang>)
	(<pred> ^name talk-waiting-answer ^object <obj>) 
	(<obj> ^input-link <il> ^label <label>)
	-(<il> ^visible {<<|now| |just|>>})
-->
	(<s> ^operator <op> +, =)
	(<op> ^name searching-missing-speaker ^substate flexbe ^process-args <a> ^interruptable hard)
	(<a> ^object <obj> ^behavior ExecuteJointTrajectoryAndSay ^joint_trajectory |head_look_around| ^text (concat |Where are you, interlocutor | <label> |?|) ^lang <lang>)
}

# continue search until speaker is not visible
sp {talk-llm*propose*seaching-missing-speaker-wait
	(state <s> ^substate talk-llm ^searching-missing-speaker <proc>)
	(<proc>  ^object.input-link <obj-il>)
	-(<il> ^visible {<<|now| |just|>>})
-->
	(<s> ^operator <o> +, =)
	(<o> ^name searching-missing-speaker-wait ^substate flexbe ^process-link <proc>)
}

# abort immediatelly is speaker is found
# more high level
sp {talk-llm*propose*seaching-missing-speaker-found
	(state <s> ^substate talk-llm ^searching-missing-speaker <proc> ^io.output-link.flexbe <cmd>)
	(<proc>  ^object.input-link.visible {<<|now| |just|>>})
	(<cmd> -^status -^abort ^process <proc>)
-->
	(<s> ^operator <op> +)
	(<op> ^name searching-missing-speaker-abort ^type automatic cognition)
	(<op> ^parent <cmd> ^attribute abort ^link-value hard)
}

# add missing predicate if speaker is not found
sp {talk-llm*propose*missing-speaker-is-not-found
	(state <s> ^substate talk-llm ^beliefs <b> ^object <obj>)
	(<b> ^event <ev>)
	(<ev> ^name searching-missing-speaker ^reason terminated ^status succeed ^process.object <obj>)
	(<obj> ^input-link <obj-il>)
	(<obj-il> -^visible now)
	-{
	  (<b> ^predicate <pred>)
	  (<pred> ^name is-missing ^object <obj>)
	}
-->
	(<s> ^operator <op> +, =)
	(<op> ^name speaker-is-missing ^type cognition automatic)
	(<op> ^add-predicate <pred-new>)
	(<pred-new> ^name is-missing ^object <obj> ^text |Human leaved abruply during previous conversation. Sweeie does not know why he did this.| ^deadline-time 600.0)
}

#
# COGNITION: talk-heard, talk-illegible
#

# predicate symbolizing speech condition
sp {talk-llm*elaborate*hearing-speech-preicate
	(state <s> ^top-state 1 ^io.input-link.sound <snd> ^beliefs <b>)
	(<snd> ^speech 1)
-->
	(<b> ^predicate <pred>)
	(<pred> ^name hearing-speech ^input-link <snd>)
}

# speech end detected
sp {talk-llm*elaborate*hearing-speech*terminate
	(state <s> ^bottom-state 1 ^beliefs.event <ev>)
	(<ev> ^name hearing-speech ^reason terminated -^processed add-talk-event ^predicate.input-link <snd>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name add-talk-event ^type cognition automatic ^input-link <snd> ^add-event <ev-new> ^mark-processed <ev>)
}

# speech is not decoded properly
sp {talk-llm*elaborate*hearing-speech*terminate*illegible
	(state <s> ^operator <op> +)
	(<op> ^name add-talk-event ^input-link <snd> ^add-event <ev-new>)
	(<snd> -^text)
-->
	(<ev-new> ^name talk-illegible ^memorization-time 3600.0)
	(<op> ^print (concat (crlf) (crlf) |TALK-ILLEGIBLE.| (crlf)))
}

# speech is decoded properly
sp {talk-llm*elaborate*hearing-speech*terminate*heard
	(state <s> ^operator <op> +)
	(<op> ^name add-talk-event ^input-link <snd> ^add-event <ev-new>)
	(<snd> ^text <text> ^lang <lang>)
-->
	(<ev-new> ^name talk-heard ^text <text> ^lang <lang> ^memorization-time 3600.0)
	(<op> ^print (concat (crlf) (crlf) |TALK-HEARD: | <text> |(| <lang> |)| (crlf)))
}

# copy extracted tokens
sp {talk-llm*elaborate*hearing-speech*terminate*elements
	(state <s> ^operator <op> +)
	(<op> ^name add-talk-event ^input-link.nlp <nlp> ^add-event <ev-new>)
	(<nlp> ^element <value>)
-->
	(<ev-new> ^token <value>)
}

# speech source: sound module, human 
sp {talk-llm*propose*talk-heard*speaker-detection-sound
	(state <s> ^bottom-state 1 ^beliefs <b> ^io.input-link.sound <snd>)
	(<b> ^event <ev> ^object <obj>)
	(<ev> ^name {<<|talk-heard| |talk-illegible|>>} -^object)
	(<snd> ^best-speech-source.swm-object <swm>)
	(<obj> ^input-link <swm> ^label <label> ^type {<<|human| |human-supposed|>>})
-->
	(<s> ^operator <op> +, =)
	(<op> ^name speaker-detection-sound ^type cognition automatic)
	(<op> ^parent <ev> ^attribute object ^link-value <obj>)
	(<op> ^print (concat (crlf) (crlf) |TALK-EVENT HUMAN from sound: | <label> (crlf)))
}

# speech source: sound module, undetected source
#sp {talk-llm*propose*talk-heard*speaker-detection-sound-unknown
#	(state <s> ^bottom-state 1 ^beliefs <b> ^io.input-link.sound.best-speech-source.swm-object <swm>)
#	(<swm> ^type speech)	
#	(<b> ^event <ev>)
#	(<ev> ^name talk-heard -^object ^initiated-at <now>)
#    -(<b> ^object.input-link <swm>)
#-->
#	(<s> ^operator <op> +, =)
#	(<op> ^name speaker-detection-sound ^type cognition automatic)
#	(<op> ^parent <b> ^attribute object ^copy-value-one-level <obj>)
#	(<obj> ^label unknown-speaker ^type human-supposed ^input-link <swm> ^terminate-time (+ <now> 10.0))
#	(<op> ^print (concat (crlf) (crlf) |TALK-EVENT UNKNOWN from sound: | <label> (crlf)))
#}

# speech source: visible human
sp {talk-llm*propose*talk-heard*speaker-detection-visible-human
	(state <s> ^substate talk-llm ^beliefs <b>)
	(<b> ^event <ev> ^object <obj>)
	(<ev> ^name {<<|talk-heard| |talk-illegible|>>} -^object)
	(<obj> ^input-link.visible now ^label <label> ^type human)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name speaker-detection-visible-human ^type cognition automatic ^object <obj>)
	(<op> ^parent <ev> ^attribute object ^link-value <obj>)
	(<op> ^print (concat (crlf) (crlf) |TALK-EVENT SOURCE from vision: | <label> (crlf)))
}

sp {talk-llm*prefer*talk-heard*speaker-detection-sound
	(state <s> ^substate talk-llm ^operator <op1> + ^operator <op2> +)
	(<op1> ^name speaker-detection-sound)
	(<op2> ^name speaker-detection-visible-human)
-->
	(<s> ^operator <op1> > <op2>)
}

sp {talk-llm*elaborate*speaker-detection-visible-human*most-recent
	(state <s> ^substate talk-llm ^operator <op> +  ^most-recent-event.object <obj>)
	(<op> ^name speaker-detection-visible-human ^object <obj>)
-->
	(<op> ^object-class most-recent-event)
}

sp {talk-llm*prefer*talk-heard*speaker-detection-visible-human*most-recent
	(state <s> ^substate talk-llm ^operator <op1> + ^operator {<> <op1> <op2>} +)
	(<op1> ^name speaker-detection-visible-human ^object-class most-recent-event)
	(<op2> ^name speaker-detection-visible-human -^object-class most-recent-event)
-->
	(<s> ^operator <op1> > <op2>)
}

sp {talk-llm*prefer*talk-heard*speaker-detection-visible-human*center
	(state <s> ^substate talk-llm ^operator <op1> + ^operator {<> <op1> <op2>} +)
	(<op1> ^name speaker-detection-visible-human -^object-class most-recent-event ^object.input-link.yaw-head center)
	(<op2> ^name speaker-detection-visible-human -^object-class most-recent-event ^object.input-link.yaw-head {<> center})
-->
	(<s> ^operator <op1> > <op2>)
}

#
# COGNITION: talk-ignored
# 

# initiate predicate at talk said
sp {talk-llm*propose*add-talk-waiting-answer
	(state <s> ^bottom-state 1 ^beliefs <b> ^io.input-link.sound.speech 0)
	(<b> ^event <ev>)
	(<ev> ^name talk-said ^object <obj>)
	-{
	  (<b> ^predicate <p>)
	  (<p> ^name talk-waiting-answer ^object <obj>)
	}
--> 
	(<s> ^operator <op> +, =)
	(<op> ^name add-talk-waiting-answer ^type cognition automatic ^add-predicate <pred>)
	(<pred> ^name talk-waiting-answer ^object <obj> ^event <ev> ^deadline-time 6.0)
}

# terminate predicate on speaking
sp {talk-llm*propose*terminate-talk-waiting-answer
	(state <s> ^bottom-state 1 ^beliefs.predicate <pred> ^io.input-link.sound.speech 1)
	(<pred> ^name talk-waiting-answer)
--> 
	(<s> ^operator <op> +, =)
	(<op> ^name terminate-talk-waiting-answer ^type cognition automatic ^remove-predicate <pred>)
}

# talk-ignored: speaker ignors question
sp {talk-llm*propose*add-talk-ignored
	(state <s> ^top-state 1 ^beliefs <b> ^io.input-link.sound.speech 0)
	(<b> ^event <ev>)
   	(<ev> ^name talk-waiting-answer ^reason deadline ^predicate <pred>)
	(<pred> ^object <obj> ^event <talk-said>)
	(<talk-said> ^type question)
-->
	(<b> ^event <ev-new>)
	(<ev-new> ^name talk-ignored ^object <obj> ^talk-event <talk-said> ^memorization-time 3600.0)
}

# talk-no-answer: sweaker do not answered
sp {talk-llm*propose*add-talk-no-answer
	(state <s> ^top-state 1 ^beliefs <b> ^io.input-link.sound.speech 0)
	(<b> ^event <ev>)
   	(<ev> ^name talk-waiting-answer ^reason deadline ^predicate <pred>)
	(<pred> ^object <obj> ^event <talk-said>)
	(<talk-said> ^type statement)
-->
	(<b> ^event <ev-new>)
	(<ev-new> ^name talk-no-answer ^object <obj> ^talk-event <talk-said> ^memorization-time 3600.0)
}

# detect ignorance
sp {talk-llm*propose*add-ignor-me-questions
	(state <s> ^bottom-state 1 ^beliefs <b> ^recent-events <ev-pool>)
	(<b> ^event <ev1>)
	(<ev1> ^name talk-ignored ^object <obj> ^initiated-at <t1>)
	(<ev-pool> ^event {<> <ev1> <ev2>})
	(<ev2> ^name talk-ignored ^object <obj> ^initiated-at <t2>)
	-{(<ev-pool> ^event <ev3>)
	  (<ev3> ^name talk-heard  ^object <obj> ^initiated-at {< <t1> > <t2>})
	}
	-{(<b> ^predicate <pred>)
	  (<pred> ^name ignors-me ^actor <obj>)
	} 
-->
	(<s> ^operator <op> +, =)
	(<op> ^name add-ignors-me ^type cognition automatic ^add-predicate <pred-ignors>)
	(<pred-ignors> ^name ignors-me ^actor <obj> ^deadline-time 120.0)
	(<pred-ignors> ^text |Human ignors Sweetie. He did not answer questions.|)
}

# detect ignorance
sp {talk-llm*propose*add-ignor-statements
	(state <s> ^bottom-state 1 ^beliefs <b> ^recent-events <ev-pool>)
	(<b> ^event <ev1>)
	(<ev1> ^name talk-no-answer ^object <obj> ^initiated-at <t1>)
	(<ev-pool> ^event {<> <ev1> <ev2>} ^event {<> <ev1> <> <ev2> <ev3>})
	(<ev2> ^name {<<|talk-ignored| |talk-no-answer|>>} ^object <obj> ^initiated-at <t2>)
	(<ev3> ^name {<<|talk-ignored| |talk-no-answer|>>} ^object <obj> ^initiated-at {< <t2> <t3>})
	-{(<ev-pool> ^event <ev4>)
	  (<ev4> ^name talk-heard  ^object <obj> ^initiated-at {< <t1> > <t3>})
	}
	-{(<b> ^predicate <pred>)
	  (<pred> ^name ignors-me ^actor <obj>)
	} 
-->
	(<s> ^operator <op> +, =)
	(<op> ^name add-ignors-me ^type cognition automatic ^add-predicate <pred-ignors>)
	(<pred-ignors> ^name ignors-me ^actor <obj> ^deadline-time 120.0)
	(<pred-ignors> ^text |Human ignored Sweetie. He did not participate in dialog.|)
}

#
# COGNITION: ponies, gestures 
#

# detect plusie
#sp {talk-llm*elaborate*speaker-gesture
#	(state <s> ^top-state 1  ^beliefs <b>)
#	(<b> ^object <obj> ^process <proc>)
#	(<obj> ^type hand ^label {<<|brohoof| |hand_greeting|>>} ^input-link <il>)
#	(<il> ^visible now ^yaw-head center)
#	(<proc> ^substate talk-llm ^object <speaker>)
#-->
#	(<b> ^predicate <pred>)
#	(<pred> ^name greet-us-with-gesture ^actor <speaker>)
#	(<pred> ^text |Human greets Sweetie with gesture.|)
#}
#
#sp {talk-llm*elaborate*speaker-pony
#	(state <s> ^top-state 1  ^beliefs <b>)
#	(<b> ^object <obj> ^process <proc>)
#	(<obj> ^type pony  ^input-link <il>)
#	(<il> ^visible now ^yaw-head center)
#	(<proc> ^substate talk-llm ^object <speaker>)
#-->
#	(<b> ^predicate <pred>)
#	(<pred> ^name greet-us-with-gesture ^actor <speaker>)
#	(<pred> ^text |Human is holding pony plusie.|)
#}


