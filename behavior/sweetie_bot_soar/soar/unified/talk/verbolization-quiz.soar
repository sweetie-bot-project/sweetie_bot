#
# PREDIACTES VERBOLIZATION
#
# Utility levels:
#
# 1. 20 --- general predicates verbolization, second verbolization
# 2. 20 --- general questions
# 3. 40-50 --- greeting and making acquitance
# 4. 70 --- general knowledge additions
# 5. 80 --- reactions
#
# QUIZ IMPLEMENTATIAON
#
# Predicates
#
# quiz preidicate: (<pred> ^name quiz ^number-of-asked-questions <number> ^score <score> ^object <talker>)
# quiz answer predicate: (<q1> ^name quiz-answer ^object <speaker> ^question <link-to-question> ^result "correct|incorrect|ignored")
# quiz finished preidicate: (<pred> ^name quiz-finished ^score <score> ^object <talker>
#
# Memory
#
# question structure
# (^quiz <quiz>)
# 	(<quiz> ^question <q>)
#		(<v> ^question <question-text> ^statement <statement-text> ^answer <a1> <a2>)
#			(<a1> ^reaction <reaction-text> ^result "correct-answer|incorrect-answer|skip-question|repeat-question")
#
# Score
#     correct +0.25
# 	  incorrect -0.25
#     ignored -0.125
#
# Total Score
#     success 1
#     normal > 0.5 < 1
#     bad < 0.5
#
#

# GENERAL KNOWLEDGE

# if quiz topic is ignored then stop quiz
sp {quest*propose*ignor-topic-terminate-quiz
	(state <s> ^bottom-state 1 ^beliefs <b>)
	(<b> ^predicate <pred1> <pred2>)
	(<pred1> ^name quiz ^object <obj>)
	(<pred2> ^name ignor-topic ^actor <obj> ^topic quiz)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name ignor-topic-terminate-quiz ^type cognition automatic ^remove-predicate <pred1>)
}

# VERBOLIZATION RULES

#
# ask speaker about quiz
#

# ask if speaker is interest in quiz
sp {quiz*elaborate*ask-about-quiz
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
  -{(<b> ^predicate <pred1>) (<pred1> ^name quiz ^object <obj>)}
  -{(<b> ^predicate <pred2>) (<pred2> ^name quiz-finished ^object <obj>)}
-->
	(<v> ^frame <f>)
	(<f> ^topic ask-about-quiz ^utility 20 ^type question ^animation-tag question ^text |Хочешь поучастввать в викторине?|)
}

# ask about quiz again: select phares based on result
sp {quiz*elaborate*ask-about-quiz-again
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
  	(<b> ^predicate <pred>)
	(<pred> ^name quiz-finished ^object <obj> ^score <score>)
-->
	(<v> ^frame <f>)
	(<f> ^topic ask-about-quiz ^utility 20 ^type question ^animation-tag question ^quest-finised-score <score>)
}

sp {quiz*elaborate*ask-about-quiz-again*prefect
	(state <s> ^substate talk ^verbolization.frame <f>)
	(<f> ^topic ask-about-quiz ^quest-finised-score 1.0)
-->
	(<f> ^text |Хочешь снова поучаствовать в викторине? Твой результат был идеален, но, может, найдейтся что-то, что ты не знаешь?|)
}

sp {quiz*elaborate*ask-about-quiz-again*good
	(state <s> ^substate talk ^verbolization.frame <f>)
	(<f> ^topic ask-about-quiz ^quest-finised-score {< 1.0 > 0.1})
-->
	(<f> ^text |Хочешь снова поучаствовать в викторине? Ты неплохо отвечал, но не все ответы были верными.|)
}

sp {quiz*elaborate*ask-about-quiz-again*bad
	(state <s> ^substate talk ^verbolization.frame <f>)
	(<f> ^topic ask-about-quiz ^quest-finised-score {<= 0.1  > -1.0})
-->
	(<f> ^text |Хочешь снова поучаствовать в викторине? Твой прошлый результат был... не очень.|)
}

sp {quiz*elaborate*ask-about-quiz-again*abysmal
	(state <s> ^substate talk ^verbolization.frame <f>)
	(<f> ^topic ask-about-quiz ^quest-finised-score -1.0)
-->
	(<f> ^text |Хочешь снова поучаствовать в викторине? Твой прошлый результат был так плох, что я подозреваю, что ты отвечал неправильно специально.|)
}

# speaker answer processing
sp {talk-answered*elaborate*ask-about-quiz*reaction
	(state <s> ^substate talk-answered ^question-topic ask-about-quiz)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-speaker-agreed-to-quiz ^type reaction ^text |Отлично! Тогда я начинаю!| ^animation-tag confirmation)
	(<r2> ^answer-topic no ^topic reaction-speaker-agreed-to-quiz ^type reaction ^text |неуверен в своих знаниях? Ну, может в другой раз.| |Не хочешь -- как хочешь.| ^animation-tag rejection)
}

# initiate quiz
sp {talk-answered*propose*ask-about-quiz*yes
	(state <s> ^substate talk-answered ^question-topic ask-about-quiz ^answer-topic yes ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name quiz ^object <obj> ^number-of-asked-questions 0 ^number-of-questions 3 ^score 0.0 ^deadline-time 600.0)
}

# clear previous results
# TODO: do it after new quiz is finished
sp {talk-answered*propose*ask-about-quiz*yes*terminate-quiz-finished
	(state <s> ^substate talk-answered ^question-topic ask-about-quiz ^answer-topic yes ^actor <obj> ^beliefs <b> ^actions <a>)
  	(<b> ^predicate <pred>)
	(<pred> ^name quiz-finished ^actor <obj>)
-->
	(<a> ^remove-predicate <pred>)
}

# do not ask about quiz again if speaker rejected it
sp {talk-answered*propose*ask-about-quiz*no
	(state <s> ^substate talk-answered ^question-topic ask-about-quiz ^answer-topic no ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name reject-talk-topic ^object <obj> ^topic ask-about-quiz ^deadline-time 60.0)
}

# prepend next quiz phrase with pharse about reterning to quiz
sp {quiz*elaborate*say-about-returning-to-quiz
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred>)
	(<pred> ^name quiz ^object <obj>)
	(<s> -^last-said-topic {<<|quiz| |say-return-to-quiz| |reaction-speaker-agreed-to-quiz|>>})
-->
	(<v> ^frame <f>)
	(<f> ^topic say-return-to-quiz ^utility 31 ^type reaction ^animation-tag none ^text |Я продолжу наш опрос!| |Вернемся к викторине, который мы прервали.|)
}

#
# speaker initiated quiz
#

# ask speaker if he want to play in quiz
sp {talk-heard*elaborate*quiz-requiz-during-question
	(state <s> ^substate talk-heard ^topic quiz ^waiting-answer-type question ^actor <obj> ^beliefs <b>)
	-{(<b> ^predicate <pred2>)
	  (<pred2> ^name quiz ^object <obj>)}
-->
	(<s> ^reaction <f>)
	(<f> ^topic ask-about-quiz ^type question ^animation-tag question ^text |Избегаешь ответа, переводишь тему, чтобы поиграть в викторину? |)
}

# ask speaker if he want to play in quiz
sp {talk-heard*elaborate*quiz-requiz-during-statement
	(state <s> ^substate talk-heard ^topic quiz -^waiting-answer-type question ^actor <obj> ^beliefs <b>)
	-{(<b> ^predicate <pred2>)
	  (<pred2> ^name quiz ^object <obj>)}
-->
	(<s> ^reaction <f>)
	(<f> ^topic ask-about-quiz ^type question ^animation-tag question ^text |Хочешь пройти опрос?|)
}

#
# propose contine quiz interupted long time ago
#
sp {quiz*elaborate*ask-about-returning-to-quiz
	(state <s> ^substate talk ^beliefs <b> ^object <obj>  ^events <ev-pool> ^verbolization <v> ^time <clock>)
	(<b> ^predicate <pred>)
	(<pred> ^name quiz ^object <obj>)
	(<s> -^last-said-topic {<<|quiz| |say-return-to-quiz| |reaction-speaker-agreed-to-quiz|>>})
	-{(<ev-pool> ^event <ev>)
	  (<ev> ^name talk-said ^topic quiz ^object <obj> ^initiated-at <time>)
	  (<clock> ^recently {< <time>})
    }
-->
	(<v> ^frame <f>)
	(<f> ^topic ask-about-returning-to-quiz ^utility 31 ^type question ^animation-tag question ^text |Наша викторина прервалась. Может, продолжим?|)
}

sp {talk-answered*elaborate*ask-about-returning-to-quiz*reaction
	(state <s> ^substate talk-answered ^question-topic ask-about-returning-to-quiz)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-speaker-agreed-to-quiz ^type reaction ^text |Отлично! Тогда я начинаю!| ^animation-tag confirmation)
	(<r2> ^answer-topic no ^topic reaction-speaker-agreed-to-quiz ^type reaction ^text |Тогда я сброшу твой прогресс.| |Не хочешь -- как хочешь. В следующий раз начнешь заново.| ^animation-tag rejection)
}

sp {talk-answered*propose*ask-continue-quiz-for-other-speaker*yes
	(state <s> ^substate talk-answered ^question-topic ask-about-returning-to-quiz ^answer-topic yes ^actor <obj> ^beliefs <b> ^actions <a> ^time.now <now>)
	(<b> ^predicate <pred>)
	(<pred> ^name quiz ^object <obj> ^initiated-at <old>)
-->
	(<a> ^parent <pred> ^attribute initiated-at ^remove-value <old> ^link-value <now>)
}

sp {talk-answered*propose*ask-about-returning-to-quiz*no
	(state <s> ^substate talk-answered ^question-topic ask-about-returning-to-quiz ^answer-topic no ^actor <obj> ^beliefs <b> ^actions <a>)
	(<b> ^predicate <pred>)
	(<pred> ^name quiz ^object <obj>)
-->
	(<a> ^remove-predicate <pred>)
}

# QUIZ IMPLEMENTATION

# elaborate quiz subspace in talk substate
sp {quiz*elaborate*quiz-subspace
	(state <s> ^substate talk ^object <obj> ^beliefs.predicate <pred>)
	(<pred> ^name quiz ^object <obj>)
-->
   	(<s> ^quiz <q>)
}

# if quiz predicate is not agumented with current question select it
sp {quiz*propose*possible-question
	(state <s> ^substate talk ^object <obj> ^beliefs.predicate <pred> ^mem.quiz.question <q>)
	(<pred> ^name quiz ^object <obj> -^current-question ^number-of-asked-questions <k> ^number-of-questions {> <k>})
-->
   (<s> ^operator <op> +, =)
   (<op> ^name select-quiz-question ^type cognition automatic ^question <q>)
   (<op> ^parent <pred> ^attribute current-question ^link-value <q>)
}

# prefer questions which os not asked yet
sp {quiz*prefer*not-asked-questions
	(state <s> ^substate talk ^operator <op1> + ^operator {<> <op1> <op2>} + ^object <obj> ^beliefs <b>)
	(<op1> ^name select-quiz-question ^question <question1>)
	(<b> ^predicate <pred1>)
	(<pred1> ^name quiz-answer ^object <obj> ^question <question1>)
	(<op2> ^name select-quiz-question ^question <question2>)
    -{(<b> ^predicate <pred2>)
	  (<pred2> ^name quiz-answer ^object <obj> ^question <question2>)
	}
-->
	(<s> ^operator <op1> < <op2>)
}

# translate quiz question to verbolize frame
sp {quiz*elaborate*question-frame
	(state <s> ^substate talk ^beliefs.predicate <pred> ^object <obj> ^verbolization <v>)
	(<pred> ^name quiz ^object <obj> ^current-question <q>)
   	(<q> ^question <text>)
-->
	(<v> ^frame <f>)
	(<f> ^topic quiz ^type question ^utility 30 ^text <text> ^animation-tag question)
}

# if quiz-answer initiated then deassotiate current question
sp {quiz*propose*deaasotiate-answered-question
	(state <s> ^substate talk ^object <obj> ^beliefs <b>)
	(<b> ^predicate <pred1> ^event <ev2>)
	(<pred1> ^name quiz ^object <obj> ^current-question <q>)
	(<ev2> ^name quiz-answer ^reason initiated ^predicate <pred2>)
	(<pred2> ^name quiz-answer ^object <obj> ^question <q>)
-->
   (<s> ^operator <op> +, =)
   (<op> ^name deassotiate-quiz-question ^type cognition automatic)
   (<op> ^parent <pred1> ^attribute current-question ^remove-value <q>)
}

# all question was asked: translate quiz predicate to quiz-finished
sp {quiz*propose*quiz-finished
	(state <s> ^substate talk ^object <obj> ^beliefs.predicate <pred>)
	(<pred> ^name quiz ^object <obj> ^score <score> -^current-question ^number-of-asked-questions <k> ^number-of-questions <k>)
-->
   (<s> ^operator <op> +, =)
   (<op> ^name finish-quiz ^type cognition automatic ^score <score>)
   (<op> ^add-predicate <pred-fin> ^remove-predicate <pred>)
   (<pred-fin> ^name quiz-finished ^object <obj> ^score <score> ^deadline-time 1200.0)
}

# translte quiz-finished predicate to verbolization frame
sp {quiz*elaorate*quiz-finished-verbolization
	(state <s> ^substate talk ^object <obj> ^beliefs.predicate <pred> ^verbolization <v>)
	(<pred> ^name quiz-finished ^object <obj> ^score <score> -^verbolized)
-->
	(<v> ^frame <f>)
	(<f> ^topic quiz-finished ^utility 30 ^quiz-score <score>)
}

sp {quiz*elaorate*quiz-finished-verbolization-perfect
	(state <s> ^substate talk ^verbolization.frame <f>)
	(<f> ^quiz-score 1.0)
-->
	(<f> ^topic quiz-finished ^type statement ^animation-tag confirmation ^text |Поздравдяю! Все выши ответы были правильными!|)
}

sp {quiz*elaorate*quiz-finished-verbolization-good
	(state <s> ^substate talk ^verbolization.frame <f>)
	(<f> ^quiz-score {< 1.0 > 0.1})
-->
	(<f> ^topic quiz-finished ^type statement ^animation-tag confirmation ^text |Викторина завершена. Неплохой результат... Но можно было лучше.|)
}

sp {quiz*elaorate*quiz-finished-verbolization-bad
	(state <s> ^substate talk ^verbolization.frame <f>)
	(<f> ^quiz-score {<= 0.1 > -1.0})
-->
	(<f> ^topic quiz-finished ^type statement ^animation-tag confirmation ^text |Викторина завершена. Возможно, вам стоит попробовать еще раз...|)
}

sp {talk-answered*propose*quiz-finished*reactions
	(state <s> ^substate talk-answered ^question-topic quiz-finished ^actor <obj> ^actions <a>)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic no ^topic quiet-finished-reaction-no ^type reaction ^text |Ты не согласен? Я помню, все твои ответы.| ^animation-tag rejection)
	(<r2> ^answer-topic yes ^topic quiet-finished-reaction-yes ^type reaction ^text |Рад, что ты со мной согласен.| ^animation-tag confirmation)
}

sp {quiz*elaorate*quiz-finished-verbolization-abysmal
	(state <s> ^substate talk ^verbolization.frame <f>)
	(<f> ^quiz-score -1.0)
-->
	(<f> ^topic quiz-finished-abysmal ^type statement ^animation-tag confirmation ^text |Вы отвечали так плохо... Что у меня подозрение, что вы делали это спецально.|)
}

sp {talk-answered*propose*quiz-finished-abysmal*reactions
	(state <s> ^substate talk-answered ^question-topic quiz-finished-abysmal ^actor <obj> ^actions <a>)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic no ^topic quiet-finished-reaction-no ^type reaction ^text |Хорошо... Но я немного разочарована.| ^animation-tag question)
	(<r2> ^answer-topic yes ^topic quiet-finished-reaction-yes ^type reaction ^text |Я знала!| ^animation-tag confirmation)
}

sp {talk-said*propose*quiz-finished-mark-verbolized
	(state <s> ^substate talk-said ^object <obj> ^topic {<<|quiz-finished| |quiz-finished-abysmal|>>} ^beliefs.predicate <pred> ^actions <a>)
	(<pred> ^name quiz-finished ^object <obj>)
-->
	(<a> ^parent <pred> ^attribute verbolized ^link-value yes)
}

# EVENT PROCESSING

# correct answer is received
sp {talk-answered*propose*quiz-question-reaction*correct-answer
	(state <s> ^substate talk-answered ^question-topic quiz ^answer-topic <answer> ^actor <obj> ^beliefs.predicate <pred> ^actions <a>)
	(<pred> ^name quiz ^object <obj> ^current-question <question> ^number-of-asked-questions <k> ^score <score> ^number-of-questions <n>)
	(<question> ^answer <ans>)
   	(<ans> ^answer-topic <answer> ^reaction <text> ^result correct-answer)
-->
	(<s> ^reaction <r>)
	(<r> ^topic quiz ^type reaction ^text <text>  ^animation-tag confirmation)
	(<a> ^parent <pred> ^attribute number-of-asked-questions ^remove-value <k> ^link-value (+ <k> 1))
	(<a> ^parent-2 <pred> ^attribute-2 score ^remove-value-2 <score> ^link-value-2 (+ <score> (/ 1.0 <n>)))
	(<a> ^add-predicate <pred-answer>)
	(<pred-answer> ^name quiz-answer ^object <obj> ^question <question> ^result correct)
}

# incorrect answer is received
sp {talk-answered*propose*quiz-question-reaction*incorrect-answer
	(state <s> ^substate talk-answered ^question-topic quiz ^answer-topic <answer> ^actor <obj> ^beliefs.predicate <pred> ^actions <a>)
	(<pred> ^name quiz ^object <obj> ^current-question <question> ^number-of-asked-questions <k> ^score <score> ^number-of-questions <n>)
	(<question> ^answer <ans>)
   	(<ans> ^answer-topic <answer> ^reaction <text> ^result incorrect-answer)
-->
	(<s> ^reaction <r>)
	(<r> ^topic quiz ^type reaction ^text <text>  ^animation-tag rejection)
	(<a> ^parent <pred> ^attribute number-of-asked-questions ^remove-value <k> ^link-value (+ <k> 1))
	(<a> ^parent-2 <pred> ^attribute-2 score ^remove-value-2 <score> ^link-value-2 (- <score> (/ 1.0 <n>)))
	(<a> ^add-predicate <pred-answer>)
	(<pred-answer> ^name quiz-answer ^object <obj> ^question <question> ^result incorrect)
}

# repeat question
sp {talk-answered*propose*quiz-question-reaction*repeat-question
	(state <s> ^substate talk-answered ^question-topic quiz ^answer-topic <answer> ^actor <obj> ^beliefs.predicate <pred>)
	(<pred> ^name quiz ^object <obj> ^current-question <question>)
	(<question> ^answer <ans>)
   	(<ans> ^answer-topic <answer> ^reaction <text> ^result repeat-question)
-->
	(<s> ^reaction <r>)
	(<r> ^topic quiz ^type reaction ^text <text> ^animation-tag confirmation)
}

# skip question
sp {talk-answered*propose*quiz-question-reaction*skip-question
	(state <s> ^substate talk-answered ^question-topic quiz ^answer-topic <answer> ^actor <obj> ^beliefs.predicate <pred> ^actions <a>)
	(<pred> ^name quiz ^object <obj> ^current-question <question>)
	(<question> ^answer <ans>)
   	(<ans> ^answer-topic <answer> ^reaction <text> ^result skip-question)
-->
	(<s> ^reaction <r>)
	(<r> ^topic quiz ^type reaction ^text <text> ^animation-tag rejection)
	(<a> ^add-predicate <pred-answer>)
	(<pred-answer> ^name quiz-answer ^object <obj> ^question <question> ^result ignored)
}

# repeat question if it is ignored
sp {talk-ignored*propose*quiz-question-ignored
	(state <s> ^substate talk-ignored ^question-topic quiz ^actor <obj> ^beliefs.predicate <pred> ^actions <a>)
	(<pred> ^name quiz ^object <obj> ^current-question.statement <text>)
-->
	(<s> ^reaction <r>)
	(<r> ^topic quiz ^type question ^text <text> ^animation-tag anger)
}
