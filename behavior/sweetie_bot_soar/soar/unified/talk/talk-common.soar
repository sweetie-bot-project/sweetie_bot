#
# TALKING: COMMON EVENT PROCESSING CODE
#

# ELABORATIONS

# substate basic agumentation
sp {talk-event*elaborate*event
	(state <s> ^substate talk-event ^superstate.operator <sop>)
	(<sop> ^event <ev>)
-->
	(<s> ^event <ev>)
	(<s> ^actions <a>)
	(<s> ^talk-ignored <c1> ^talk-answered <c2> ^talk-irrelevant <c3>)
}

# elaborate recent talk events
sp {talk-event*elaborate-recent-events
	(state <s> ^substate talk-event ^event.actor <obj> ^events.event <ev> ^time.recently <time>)
	(<ev> ^name {<<|talk-ignored| |talk-answered| |talk-irrelevant|>> <ev-type>} ^actor <obj> ^initiated-at {>= <time>})	
	(<s> ^<ev-type> <c>)
-->
	(<c> ^event <ev>)
}

# check if we are waiting answer (talk-answered event substate have special routines)
sp {talk-event*elaborate*waiting-answer
	(state <s> ^substate talk-event -^substate talk-answered ^beliefs.process <proc> ^actor <obj>)
	(<proc> ^name waiting-answer ^substate talk ^topic <topic> ^object <obj>)
-->
	(<s> ^last-topic <topic>)
	(<s> ^question-topic <topic>)
	(<s> ^waiting-answer <proc>)
}

# check if we are waiting reaction
sp {talk-event*elaborate*waiting-reaction
	(state <s> ^substate talk-event ^beliefs.process <proc> ^actor <obj>)
	(<proc> ^name waiting-reaction ^substate talk ^topic <topic> ^object <obj>)
-->
	(<s> ^last-topic <topic>)
	(<s> ^statement-topic <topic>)
	(<s> ^waiting-reaction <proc>)
}

## add last topic
#sp {talk-heard*elaborate*last-topic
#	(state <s> ^substate talk-heard ^beliefs.predicate <proc> ^actor <obj>)
#	(<proc> ^name last-topic ^topic <topic> ^object <obj>)
#-->
#	(<s> ^last-topic <topic>)
#}


# DEBUG

# ptint topic and conclusion
sp {talk-common*propose*debug-output
	(state <s> ^substate talk-event ^substate {<> |talk-event| <ev-type>} ^topic <topic> ^conclusion <conclusion> ^actions <a>)
-->
	(<a> ^print (concat <ev-type> |: topic '| <topic> |', conclusion '| <conclusion> |'|))
}

# COGNITION RULES

# worst operator: cognition process is finished
sp {talk-event*propose*finish
	(state <s> ^substate talk-event ^event <ev>)
-->
	(<s> ^operator <op> +, <)
	(<op> ^name talk-event-reaction ^type automatic ^mark-processed <ev>)
}

# add all actions to finish-event-processing operator
sp {talk-event*elaborate*finish-agumentation
	(state <s> ^substate talk-event ^operator <op> + ^actions <a>)
	(<op> ^name talk-event-reaction)
	(<a> ^<attr> <value>)
-->
	(<op> ^<attr> <value>)
}

