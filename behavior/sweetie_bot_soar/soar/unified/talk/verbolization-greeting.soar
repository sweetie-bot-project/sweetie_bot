#
# PREDIACTES VERBOLIZATION
#
# Perform verblization of talk-related predicates
#
# * (<pred> ^name possible-talk-topic ^object <obj> ^topic <a-topic> ^previous-topic <q-topic> ^reaction talk-answered ^deadline-time 60.0) --- possible reaction to
# *	(<pred> ^name ignor-possibility ^actor <obj> ^topic <topic> ^deadline-time 60.0) --- question is possibly being ignored.
# *	(<pred> ^name ignor-topic ^actor <obj> ^topic <topic> ^deadline-time 600.0) --- question on specific topic is being ignored.
# *	(<pred> ^name ignor-me ^actor <obj>) --- speaker ignores me.
# *	(<pred> ^name nonsense-possibility ^actor <obj> ^topic <topic> ^deadline-time 60.0) --- answer has no meaning
# *	(<pred> ^name nonsense-me ^actor <obj>) --- speaker doe not interact normally
#
# Utility levels:
#
# 1. 20 --- general predicates verbolization, second verbolization
# 2. 20 --- general questions
# 3. 40-50 --- greeting and making acquitance
# 4. 70 --- general knowledge additions
# 5. 80 --- reactions
#

# GENERAL KNOWLEDGE RULES

# remove greeted predicate if object disappered for more then 2 minutes
sp {verbolization*propose*remove-greeted-predicate
	(state <s> ^bottom-state 1 ^beliefs.predicate <pred> ^time <clock>)
	(<pred> ^name greeted ^object <obj>)
	(<obj> ^preceive-end <time>)
	-(<clock> ^recently <= <time>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name remove-greeted-predicate ^type cognition automatic ^remove-predicate <pred>)
}

# dislike speaker
sp {verbolization*elaborate*dislike-from-ignor-me-nonsense-me
	(state <s> ^top-state 1 ^beliefs <b>)
	(<b> ^predicate <pred>)
	(<pred> ^name {<<|ignor-me| |nonsense-me|>>} ^actor <obj>)
-->
	(<b> ^predicate <pred-new>)
	(<pred-new> ^name dislike-for ^object <obj> ^actor me ^reason <pred>)
}

# VERBOLIZATION RULES

#
# greeting
#

# elabotare greeting frame base
sp {verbolization*elaborate*greeting-base
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	-{(<b> ^predicate <pred1>)
	  (<pred1> ^name greeted ^object <obj>)}
-->
	(<v> ^frame <f>)
	(<f> ^topic greeting ^type question)
}

# if not acquiatance
sp {verbolization*elaborate*greeting-not-acquitance
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization.frame <f>)
	(<f> ^topic greeting ^type question)
	-{(<b> ^predicate <pred2>)
	  (<pred2> ^name acquitance ^object <obj>)}
-->
	(<f>  ^utility 50 ^animation-tag greeting)
	(<f> ^text |Привет! Я --- Свити. Лучшая робопони в мире.| |Привет! Меня зовут Свити Бот!|)
}

sp {verbolization*elaborate*greeting-not-acquitance*reaction
	(state <s> ^substate talk-answered ^question-topic greeting ^answer-topic greeting ^actor <obj> ^beliefs <b>)
	-{(<b> ^predicate <pred2>)
	  (<pred2> ^name acquitance ^object <obj>)}
	(<obj> ^label <label>)
-->
	(<s> ^reaction <r1>)
	(<r1> ^answer-topic greeting ^topic make-acquitance ^text (concat |Я буду называть тебя собеседник | <label> |.|) ^animation-tag confirmation)
}

# if speaker is aready known to robot
sp {verbolization*elaborate*greeting-acquitance-normal
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization.frame <f>)
	(<f> ^topic greeting ^type question)
	(<b> ^predicate <pred2>)
	(<pred2> ^name acquitance ^object <obj>)
	(<obj> ^label <label>)
-->
	(<f> ^utility 50 ^animation-tag greeting)
	(<f> ^text (concat |Привет, собеседник | <label> |!|) (concat |Рада тебя видеть вновь, собеседник | <label> |.|))
}

sp {verbolization*elaborate*greeting-acquitance-dislike
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization.frame <f>)
	(<f> ^topic greeting)
	(<b> ^predicate <pred2>)
	(<pred2> ^name dislike-for ^object <obj>)
	(<obj> ^label <label>)
-->
	(<f> ^utility 55 ^type question ^animation-tag rejection)
	(<f> ^text (concat |Привет, собеседник | <label> |!| |Но я тобой недовольна.|) (concat |Не очень рада тебя видеть вновь, собеседник | <label> |.|))
}

sp {talk-said*propose*greeted
	(state <s> ^substate talk-said ^topic greeting ^beliefs <b> ^object <obj> ^actions <a>)
	-{(<b> ^predicate <pred>) (<pred> ^name greeted ^object <obj>)}
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name greeted ^object <obj> ^counter 1 ^deadline-time 600.0)
}

sp {talk-said*propose*greeted-add-counter
	(state <s> ^substate talk-said ^topic greeting ^beliefs.predicate <pred> ^object <obj> ^actions <a>)
	(<pred> ^name greeted ^object <obj> ^counter <n>)
-->
	(<a> ^parent <pred> ^attribute counter ^remove-value <n> ^link-value (+ <n> 1))
}

#
# speaker initiated greeting
#

sp {talk-heard*elaborate*greeting-not-acquitance
	(state <s> ^substate talk-heard ^topic greeting ^actor <obj> ^beliefs <b>)
	-{(<b> ^predicate <pred2>)
	  (<pred2> ^name acquitance ^object <obj>)}
-->
	(<s> ^reaction <f>)
	(<f> ^topic greeting ^type reaction ^animation-tag greeting)
	(<f> ^text |Привет! Я --- Свити. Лучшая робопони в мире.| |Привет! Меня зовут Свити Бот!|)
}

sp {talk-heard*elaborate*greeting-acquitance-not-greeted
	(state <s> ^substate talk-heard ^topic greeting ^actor <obj> ^beliefs <b>)
	(<b> ^predicate <pred2>)
	(<pred2> ^name acquitance ^object <obj>)
	-{(<b> ^predicate <pred1>)
	  (<pred1> ^name greeted ^object <obj>)}
	(<obj> ^label <label>)
-->
	(<s> ^reaction <f>)
	(<f> ^topic greeting ^type reaction ^animation-tag greeting)
	(<f> ^text (concat |Приветствую тебя, собеседник | <label> |.|) (concat |Выполняю приветсвие. Объект приветсвия - собеседник | <label> |.|))
}

sp {talk-heard*elaborate*greeting-acquitance-greeted
	(state <s> ^substate talk-heard ^topic greeting ^actor <obj> ^beliefs <b>)
	(<b> ^predicate <pred2> <pred1>)
	(<pred2> ^name acquitance ^object <obj>)
  	(<pred1> ^name greeted ^object <obj> ^counter {<= 3})
	(<obj> ^label <label>)
-->
	(<s> ^reaction <f>)
	(<f> ^topic greeting ^type reaction ^animation-tag greeting)
	(<f> ^text (concat |Тебе нравится здороваться со мной, собеседник | <label> |? Привет! Привет! Привет!|) (concat |Запрос на повторное приветсвие принят. Здравствуй, собеседник | <label> |.|))
}

sp {talk-heard*elaborate*greeting-acquitance-greeted-rejection
	(state <s> ^substate talk-heard ^topic greeting ^actor <obj> ^beliefs <b>)
	(<b> ^predicate <pred2> <pred1>)
	(<pred2> ^name acquitance ^object <obj>)
  	(<pred1> ^name greeted ^object <obj> ^counter {> 3})
	(<obj> ^label <label>)
-->
	(<s> ^reaction <f>)
	(<f> ^topic greeting ^type reaction ^animation-tag anger)
	(<f> ^text |Хватит! Не хочу снова здороваться.| |Лимит приветсвий равен трем. Запрос отклонен.|)
}

# if speaker means use brohoof prefer correspoding animation
sp {talk-heard*elaborate*greeting*brohoof-tag
	(state <s> ^substate talk-heard ^topic greeting brohoof ^actor <obj> ^reaction <f>)
	(<f> ^topic greeting ^animation-tag greeting)
-->
	(<f> ^animation-tag brohoof)
}

#
# acquitance
#
sp {verbolization*elaborate*acquitance-statement
	(state <s> ^substate talk ^object <obj> ^beliefs <b> ^verbolization <v>)
	-{(<b> ^predicate <pred2>)
	  (<pred2> ^name acquitance ^object <obj>)}
	(<obj> ^label <label>)
-->
	(<v> ^frame <r1>)
	(<r1> ^topic make-acquitance ^utility 49 ^type reaction ^text (concat |Я буду называть тебя собеседник | <label> |.|) ^animation-tag confirmation)
}

sp {talk-answered*propose*made-acquitance
	(state <s> ^substate talk-said ^topic make-acquitance ^object <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name acquitance ^object <obj>)
}


#
# finish dialog
#

# default talk end reaction
sp {verbolization*elaborate*goodbye
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
-->
	(<v> ^frame <f>)
	(<f> ^topic goodbye ^utility 10 ^type question ^animation-tag question)
	(<f> ^text |У меня кончились темы для разговора. До встречи!| |Ты мне наскучил. Пока.|)
}

# register in beliefs that no questions left
sp {verbolization*elaborate*no-questions-left*initiate
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	-{(<v> ^frame <f>)
	  (<f> ^type question ^topic {<> goodbye <topic>})
	  -(<v> ^ignor-topic <topic>)
	  -(<v> ^reject-topic <topic>)
    }
	-{(<b> ^predicate <pred>)
	  (<pred> ^name no-questions-for-speaker ^object <obj>)
    }
-->
	(<s> ^operator <op> +, =)
	(<op> ^type cognition automatic ^add-predicate <pred>)
	(<pred> ^name no-questions-for-speaker ^object <obj> ^deadline-time 180.0 ^talk-deadline-time (rand-float 0.0 180.0))
}

sp {verbolization*elaborate*no-questions-left*talk-deadline
	(state <s> ^bottom-state 1 ^beliefs.predicate <pred>)
	(<pred> ^name no-questions-for-speaker ^initiated-at <itime> ^talk-deadline-time <dtime>)
-->
	(<pred> ^talk-terminate-time (+ <itime> <dtime>))
}

# remove beliefs that no questions left
sp {verbolization*elaborate*no-questions-left*terminate
	(state <s> ^substate talk ^belifs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred>)
	(<pred> ^name no-questions-for-speaker ^object <obj>)
	(<v> ^frame <f>)
	(<f> ^type question ^topic {<> goodbye <topic>})
	-(<v> ^ignor-topic <topic>)
	-(<v> ^reject-topic <topic>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^type cognition automatic ^remove-predicate <pred>)
}

# add verolization frame for no questions left for speaker
sp {verbolization*elaborate*not-questions-left-frame
	(state <s> ^substate talk ^beliefs.predicate <pred> ^object <obj> ^verbolization <v>)
	(<pred> ^name no-questions-for-speaker ^object <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic goodbye ^type question ^animation-tag question ^utility 20)
	(<f> ^text |У меня кончились темы для разговора. До встречи!| |Думаю, мы достаточно поговорили. Пока.|)
}

# if goodbye is answered in any way consider dialog finished
sp {talk-answered*propose*goodbye-end-talk
	(state <s> ^substate talk-answered ^question-topic goodbye ^actor <obj> ^beliefs.process <proc> ^actions <a>)
	(<proc> ^substate talk ^object <obj>)
-->
	(<a> ^finish-status succeed ^finish-process <proc>)
}

# if goodbye is ignored finish dialog
sp {talk-ignored*propose*goodbye-end-talk
	(state <s> ^substate talk-ignored ^topic goodbye ^actor <obj> ^beliefs.process <proc> ^actions <a>)
	(<proc> ^substate talk ^object <obj>)
-->
	(<a> ^finish-status succeed ^finish-process <proc>)
}

# end talk if speaker igonres
sp {verbolization*elaborate*goodbye-ignores-me
	(state <s> ^substate talk ^beliefs.predicate <pred> ^object <obj> ^verbolization <v> ^time <clock>)
	(<pred> ^name ignor-me ^actor <obj> ^initiated-at <time>)
	-(<clock> ^just {< <time>})
-->
	(<v> ^frame <f>)
	(<f> ^topic goodbye-reaction ^type reaction ^animation-tag question ^utility 20)
	(<f> ^text |Ты игнорируешь меня -- я игнорирую тебя! Пока.| |Думаю, мы достаточно поговорили. Пока.|)
}

# end talk if speaker says nonsense
sp {verbolization*elaborate*goodbye-nonsense-me
	(state <s> ^substate talk ^beliefs.predicate <pred> ^object <obj> ^verbolization <v> ^time <clock>)
	(<pred> ^name nonsense-me ^actor <obj> ^initiated-at <time>)
	-(<clock> ^just {< <time>})
-->
	(<v> ^frame <f>)
	(<f> ^topic goodbye-reaction ^type reaction ^animation-tag question ^utility 20)
	(<f> ^text |Не хочу с тобой разговаривать! Пока.|)
}

# end talk by request
sp {talk-heard*elaborate*goodbye-reaction
	(state <s> ^substate talk-heard ^topic goodbye ^actor <obj>)
-->
	(<s> ^reaction <f>)
	(<f> ^topic goodbye-reaction ^type reaction ^animation-tag confirmation ^utility 70)
	(<f> ^text |До встречи!| |Ты уже уходишь? Пока!|)
}

sp {talk-said*propose*goodbye-reaction-end-talk
	(state <s> ^substate talk-said ^topic goodbye-reaction ^object <obj> ^beliefs.process <proc> ^actions <a>)
	(<proc> ^substate talk ^object <obj>)
-->
	(<a> ^finish-status succeed ^finish-process <proc>)
}
