#
# PREDIACTES VERBOLIZATION
#
# Utility levels:
#
# 1. 20 --- general predicates verbolization, second verbolization
# 2. 20 --- general questions
# 3. 40-50 --- greeting and making acquitance
# 4. 70 --- general knowledge additions
# 5. 80 --- reactions
#

# GENERAL KNOWLEDGE

# dislike speaker if he dislikes me
sp {reasoning*elaborate*dislike-from-dislike-robots
	(state <s> ^top-state 1 ^beliefs <b>)
	(<b> ^predicate <pred>)
	(<pred> ^name dislike ^actor <obj> ^object robot)
-->
	(<b> ^predicate <pred-new>)
	(<pred-new> ^name dislike-for ^actor me ^object <obj> ^reason <pred>)
}

# like speaker if he likes me
sp {reasoning*elaborate*dislike-from-like-robots
	(state <s> ^top-state 1 ^beliefs <b>)
	(<b> ^predicate <pred>)
	(<pred> ^name like ^actor <obj> ^object robot)
-->
	(<b> ^predicate <pred-new>)
	(<pred-new> ^name like-for ^actor me ^object <obj> ^reason <pred>)
}

# detect contradiction
sp {reasoning*prpose*solve-contradiction-like-dislike
	(state <s> ^bottom-state 1 ^beliefs <b>)
	(<b> ^predicate <pred1>)
	(<pred1> ^name like ^actor <obj> ^object <obj>)
	(<b> ^predicate <pred2>)
	(<pred2> ^name dislike ^actor <obj> ^object <obj>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name solve-contradiction-like-dislike  ^type cognition automatic ^remove-predicate <pred1> <pred2> ^add-predicate <pred>)
	(<pred> ^name contradict-itself ^actor <obj> ^object <obj>)
}

# VERBOLIZATION RULES

#
# ask question about robots
#
sp {verbolization*elaborate*ask-do-you-like-robots
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	-{(<b> ^predicate <pred1>)
	  (<pred1> ^name {<<|like| |dislike|>>} ^actor <obj> ^object robot)}
-->
	(<v> ^frame <f>)
	(<f> ^topic do-you-like-robots ^utility 20 ^type question ^animation-tag question)
	(<f> ^text |Do you like robots?|)
}

sp {talk-answered*propose*speaker-likes-robots
	(state <s> ^substate talk-answered ^question-topic do-you-like-robots ^answer-topic yes ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name like ^actor <obj> ^object robot)
	(<s> ^reaction <r1>)
	(<r1> ^topic reaction-speaker-like-robots ^type reaction ^text |I'm glad, you admit my superiority!| ^animation-tag confirmation)
}

sp {talk-answered*propose*speaker-dilike-robots
	(state <s> ^substate talk-answered ^question-topic do-you-like-robots ^answer-topic no ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name dislike ^actor <obj> ^object robot)
	(<s> ^reaction <r>)
	(<r> ^topic reaction-speaker-dislike-robots ^type reaction ^text |Now I don't like you.| ^animation-tag rejection)
}

	
