#
# PREDIACTES VERBOLIZATION
#
# Utility levels:
#
# 1. 20 --- general predicates verbolization, second verbolization
# 2. 20 --- general questions
# 3. 40-50 --- greeting and making acquitance
# 4. 70 --- general knowledge additions
# 5. 80 --- reactions
#

# GENERAL KNOWLEDGE

# dislike speaker if he dislikes me
sp {verbolization*elaborate*dislike-from-dislike-robots
	(state <s> ^top-state 1 ^beliefs <b>)
	(<b> ^predicate <pred>)
	(<pred> ^name dislike ^actor <obj> ^type robot ^initiated-at <time>)
	-{(<b> ^predicate <pred-dislike>) 
	  (<pred-dislike> ^name dislike ^actor me ^object <obj> ^initiated-at {< <time>})}
-->
	(<b> ^predicate <pred-new>)
	(<pred-new> ^name dislike ^actor me ^object <obj>)
}

# VERBOLIZATION RULES

#
# ask question about robots
#
sp {verbolization*elaborate*ask-do-you-like-robots
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	-{(<b> ^predicate <pred1>)
	  (<pred1> ^name {<<|like| |dislike|>>} ^actor <obj> ^type robot)}
-->
	(<v> ^frame <f>)
	(<f> ^topic do-you-like-robots ^utility 20 ^type question ^animation-tag question ^reaction <r1> <r2>)
	(<f> ^text |Ты любишь роботов?|)
}

sp {verbolization*elaborate*ask-do-you-like-robots*reacton
	(state <s> ^substate talk ^last-topic do-you-like-robots ^object <obj> ^verbolization <v>)
-->
	(<v> ^reaction <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-speaker-like-robots ^text |Я рада, что ты признаешь мое превосходство!| ^animation-tag confirmation)
	(<r2> ^answer-topic no ^topic reaction-speaker-dislike-robots ^text |Теперь ты мне не нравишься.| ^animation-tag rejection)
}

sp {talk-answered*propose*speaker-likes-robots
	(state <s> ^substate talk-answered ^question-topic do-you-like-robots ^answer-topic yes ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name like ^actor <obj> ^type robot)
}

sp {talk-answered*propose*speaker-dilike-robots
	(state <s> ^substate talk-answered ^question-topic do-you-like-robots ^answer-topic no ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name dislike ^actor <obj> ^type robot)
}

	
