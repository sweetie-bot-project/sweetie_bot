# TALKING: REACTION ON ANSWER
#
# (<op> ^substate talk-answered ^type cognition ^event <ev> 
#
# Events:
#
# * (<ev-irrelevant> ^name talk-irrelevant ^actor <obj> ^topic <topic> ^memorization-time 3600.0) --- answer was irrelevant.
#
# Predicates:
#
# * (<pred> ^name possible-talk-topic ^object <obj> ^topic <a-topic> ^previous-topic <q-topic> ^reaction talk-answered ^deadline-time 60.0) --- possible reaction to 
#

# ELABORATIONS 

# propagate event attributes and substate
sp {talk-answered*elaborate*substate
	(state <s> ^substate talk-answered ^superstate.operator.event <ev>)
	(<ev> ^question-topic <q-topic> ^answer-topic <a-topic> ^actor <obj>)
-->
	(<s> ^substate talk-event)
	(<s> ^question-topic <q-topic> ^answer-topic <a-topic> ^actor <obj>)
}

# attach related waiting-answer process
sp {talk-answered*elaborate*waiting-answer
	(state <s> ^substate talk-answered ^question-topic <q-topic> ^actor <obj> ^beliefs.process <proc>)
	(<proc> ^name waiting-answer ^topic <question-topic> ^object <obj>)
-->
	(<s> ^waiting-answer <proc>)
}

# VERBOLIZATION COGNITION RULES

# propose reaction which matches answer topic and remove waiting-answer predicate
sp {talk-answered*elaboration*specific-reaction
	(state <s> ^substate talk-answered ^reaction <r> ^event <ev> ^actor <obj> ^actions <a>)
-->
	(<s> ^conclusion answer-specific-reaction)
	(<a> ^add-predicate <pred>)
	(<pred> ^name talk-possible-reaction ^type answer-specific ^object <obj> ^reaction <r> ^event <ev> ^deadline-time 60.0)
}

# propose default reaction and remove 
sp {talk-answered*elaboration*general-reaction
	(state <s> ^substate talk-answered -^reaction ^reaction-default <r> ^event <ev> ^actor <obj> ^actions <a>)
-->
	(<s> ^conclusion answer-default-reaction)
	(<a> ^add-predicate <pred>)
	(<pred> ^name talk-possible-reaction ^type answer-default ^object <obj> ^reaction <r> ^event <ev> ^deadline-time 60.0)
}

# select conditional reaction 
sp {talk-answered*elaboration*reaction-conditional
	(state <s> ^substate talk-answered ^answer-topic <topic> ^reaction-conditional <r>)
	(<r> ^answer-topic <topic>)
-->
	(<s> ^reaction <r>)
}

# select unique reaction
sp {talk-answered*elaboration*reaction-unique
	(state <s> ^substate talk-answered ^answer-topic <topic> ^reaction-unique <r> -^selected-unique-reaction)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name select-unique-rection ^type cognition automatic)
	(<op> ^parent <s> ^attribute reaction selected-unique-reaction ^link-value <r>)
}

# remove waiting-answer predicate
sp {talk-answered*propose*remove-waiting-answer
	(state <s> ^substate talk-answered ^conclusion {<<|answer-specific-reaction| |answer-default-reaction|>>} ^waiting-answer <proc> ^actions <a>)
-->
	(<a> ^remove-process <proc>)
}
