#
# PREDIACTES VERBOLIZATION
#
# Utility levels:
#
# 1. 20 --- general predicates verbolization, second verbolization
# 2. 20 --- general questions
# 3. 40-50 --- greeting and making acquitance
# 4. 70 --- general knowledge additions
# 5. 80 --- reactions
#

# QUEST CONTENT

sp {quest*elaborate*scene-meet-sweetie
	(state <s> ^top-state 1 ^beliefs.predicate <pred>)
   	(<pred> ^name quest ^scene meet-sweetie ^scene-frame <f>)
-->
	(<f> ^segment <s1> <s2> <s3> <s4> <s5>)
   	(<s1> ^id 0 ^type reaction ^animation-tag long ^next-id 1)
   	(<s1> ^text |Тихое, сонное утро в Понивилле. Ты неспешно потягиваешь травяной чай, предвкушая ленивый день отдыха.  Сегодня ты обещал Рэрити встретиться и обговорить заказ нового костюма. Тут раздается стук в дверь:|)
   	(<s2> ^id 1 ^type reaction ^animation-tag question ^next-id 2)
   	(<s2> ^text |Кого там Селестия принесла... - недовольно бурчишь ты, открывая дверь. На пороге стоит Свити Белль:|)
   	(<s3> ^id 2 ^type reaction ^animation-tag greeting ^next-id 3)
   	(<s3> ^text |Привет, друг! Извини за беспокойство, но но мне срочно нужна твоя помощь! Пойдём, объясню по дороге!|)
   	(<s4> ^id 3 ^type reaction ^animation-tag rejection ^next-id 4)
   	(<s4> ^text |Вот только у тебя другие планы, а Рэрити не будет ждать слишком долго. Единорожка спрашивает:|)
   	(<s5> ^id 4 ^type question ^animation-tag question)
   	(<s5> ^text |Ну что? Пойдешь со мной? Давай же!| ^ignore-id 5)
   	(<s5> ^id 5 ^type question ^animation-tag question)
   	(<s5> ^text |Свити спросила тебя, пойдешь ли ты с ней? Ответь да или нет.| ^ignore-id 5)
   	(<f> ^choice <c1> <c2>)
   	(<c1> ^answer-topic yes ^next-scene walk-with-sweetie)
   	(<c2> ^answer-topic no ^next-scene talk-with-rarity)
}

sp {quest*elaborate*scene-walk-with-sweetie
	(state <s> ^top-state 1 ^beliefs.predicate <pred>)
   	(<pred> ^name quest ^scene walk-with-sweetie ^scene-frame <f>)
-->
	(<f> ^segment <s1> <s2> <s3> <s4>)
   	(<s1> ^id 0 ^type reaction ^animation-tag confirmation ^next-id 1)
   	(<s1> ^text |Отлично! Я рада, что ты согласился!|)
   	(<s2> ^id 1 ^type reaction ^animation-tag general ^next-id 2)
   	(<s2> ^text |Вы двигаетесь в сторону фермы Эпплджек. Свити продолжает рассказ:|)
   	(<s3> ^id 2 ^type reaction ^animation-tag long ^next-id 3)
   	(<s3> ^text |Мы, Меткоискатели, наконец-то поняли, что гарантированно принесет нам кьютимарки. Пока наш стартап маленький, но, поверь мне, скоро мы захватим... то есть, займем достойное место в доме каждого пони!|)
   	(<s4> ^id 3 ^type reaction ^animation-tag surprise ^ending |Встретил вторую Свити|)
   	(<s4> ^text |Свити резко затихает и прячется за твою спину. Ты поднимаешь глаза и видишь точно такую же Свити Белль, стоящую на пороге домика Меткоискателей.|)
}

sp {quest*elaborate*scene-talk-with-rarity
	(state <s> ^top-state 1 ^beliefs.predicate <pred>)
   	(<pred> ^name quest ^scene talk-with-rarity ^scene-frame <f>)
-->
	(<f> ^segment <s1> <s2> <s3> <s4>)
   	(<s1> ^id 0 ^type reaction ^animation-tag rejection ^next-id 1)
   	(<s1> ^text |Ох, жаль... Понимаю, ты очень занят... Ну что ж, в другой раз.|)
   	(<s2> ^id 1 ^type reaction ^animation-tag general ^next-id 2)
   	(<s2> ^text |Свити поникает и уходит прочь, а ты отправлешься в бутик к Рэрити.|)
   	(<s3> ^id 2 ^type reaction ^animation-tag long ^ending |Встретил Рэрити|)
   	(<s3> ^text |Белоснежная единорожка, как всегда, неотразима. Сделав необходимые замеры, она приглашает тебя на чай. Ты рассказываешь о визите Свити и Рэрити меняется в лице.|)
}
	
# GENERAL KNOWLEDGE

# if quest topic is ignored then stop quest
sp {quest*propose*ignor-topic-terminate-quest
	(state <s> ^bottom-state 1 ^beliefs <b>)
	(<b> ^predicate <pred1> <pred2>)
	(<pred1> ^name quest ^object <obj>)
	(<pred2> ^name ignor-topic ^actor <obj> ^topic quest)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name ignor-topic-terminate-quest ^type cognition automatic ^remove-predicate <pred1>)
}

# VERBOLIZATION RULES

# ask if speaker is interest in quest
sp {quest*elaborate*ask-about-quest
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
  -{(<b> ^predicate <pred1>) (<pred1> ^name quest ^object <obj>)}
  -{(<b> ^predicate <pred2>) (<pred2> ^name quest-finished ^actor <obj>)}
-->
	(<v> ^frame <f>)
	(<f> ^topic ask-about-quest ^utility 20 ^type question ^animation-tag question ^text |Хочешь поиграть со мной в квест?|)
}

# ask about quest again
sp {quest*elaborate*ask-about-quest-again
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
  	(<b> ^predicate <pred>) 
	(<pred> ^name quest-finished ^actor <obj> ^ending <ending>)
-->
	(<v> ^frame <f>)
	(<f> ^topic ask-about-quest ^utility 20 ^type question ^animation-tag question ^text (concat |В прошлый раз в квесте ты получил концовку "| <ending> |". Хочешь попробовать снова?|))
}

sp {talk-answered*elaborate*ask-about-quest*reaction
	(state <s> ^substate talk-answered ^question-topic ask-about-quest)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-speaker-agreed-to-quest ^type reaction ^text |Отлично! Тогда я начинаю!| ^animation-tag confirmation)
	(<r2> ^answer-topic no ^topic reaction-speaker-agreed-to-quest ^type reaction ^text |Ты явно боишься получить плохую концовку.| |Не хочешь -- как хочешь.| ^animation-tag rejection)
}

sp {talk-answered*propose*ask-about-quest*yes
	(state <s> ^substate talk-answered ^question-topic ask-about-quest ^answer-topic yes ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name quest ^object <obj> ^scene meet-sweetie ^segment-id 0 ^scene-frame <f> ^deadline-time 600.0)
}

sp {talk-answered*propose*ask-about-quest*yes*terminate-quest-finished
	(state <s> ^substate talk-answered ^question-topic ask-about-quest ^answer-topic yes ^actor <obj> ^beliefs <b> ^actions <a>)
  	(<b> ^predicate <pred>) 
	(<pred> ^name quest-finished ^actor <obj>)
-->
	(<a> ^remove-predicate <pred>)
}

# QUEST SEGMENT VERBOLIZATION

# translate scene segment to verbolization frame
sp {quest*elaborate*verbolize-current-segment
	(state <s> ^substate talk ^beliefs.predicate <pred> ^object <obj> ^verbolization <v>)
	(<pred> ^name quest ^object <obj> ^scene <scene> ^segment-id <id> ^scene-frame.segment <segment>)
   	(<segment> ^id <id> ^type <type> ^animation-tag <tag> ^text <text>)
-->
	(<v> ^frame <f>)
	(<f> ^topic quest ^utility 30 ^type <type> ^animation-tag <tag> ^text <text>)
}

# EVENT PROCESSING

# if reaction segment was verbolized proceed to next segment: increase id by one
sp {talk-said*propose*reaction-next-segment
	(state <s> ^substate talk-said ^topic quest ^object <obj> ^beliefs.predicate <pred> ^actions <a>)
	(<pred> ^name quest ^object <obj> ^segment-id <id> ^scene-frame.segment <segment>)
   	(<segment> ^id <id> ^type reaction ^next-id <next-id>)
-->
	(<a> ^parent <pred> ^attribute segment-id  ^remove-value <id> ^link-value <next-id>)
}

# if final reaction segment was verbolized end the quest: remove quest predicate and add quest-finished predicate
sp {talk-said*propose*reaction-ending-segment
	(state <s> ^substate talk-said ^topic quest ^object <obj> ^beliefs.predicate <pred> ^actions <a>)
	(<pred> ^name quest ^object <obj> ^segment-id <id> ^scene-frame.segment <segment>)
   	(<segment> ^id <id> ^type reaction ^ending <ending>)
-->
	(<a> ^remove-predicate <pred> ^add-predicate <pred1>)
   	(<pred1> ^name quest-finished ^actor <obj> ^ending <ending>)
}

# if valid answer was received assume question answered and proceed to next scene
sp {talk-answered*propose*question-valid-answer
	(state <s> ^substate talk-answered ^question-topic quest ^answer-topic <answer> ^actor <obj> ^beliefs.predicate <pred> ^actions <a>)
	(<pred> ^name quest ^object <obj> ^scene <scene> ^segment-id <id> ^scene-frame.choice <choice>)
   	(<choice> ^answer-topic <answer> ^next-scene <next-scene>)
-->
	(<s> ^conclusion answer-specific-reaction)
	(<a> ^parent <pred> ^attribute scene ^remove-value <scene> ^link-value <next-scene>)
	(<a> ^parent-2 <pred> ^attribute-2 segment-id ^remove-value-2 <id> ^link-value-2 0)
}

# if question is ignored procced to ignore-id segment
sp {talk-answered*propose*question-ignored
	(state <s> ^substate talk-ignored ^question-topic quest ^actor <obj> ^beliefs.predicate <pred> ^actions <a>)
	(<pred> ^name quest ^object <obj> ^segment-id <id> ^scene-frame.segment <segment>)
   	(<segment> ^id <id> ^type reaction ^ignor-id <next-id>)
-->
	(<a> ^parent <pred> ^attribute segment-id  ^remove-value <id> ^link-value <next-id>)
}

