#
# PREDIACTES VERBOLIZATION
#
# Utility levels:
#
# 1. 20 --- general predicates verbolization, second verbolization
# 2. 20 --- general questions
# 3. 40-50 --- greeting and making acquitance
# 4. 70 --- general knowledge additions
# 5. 80 --- reactions
#

# GENERAL KNOWLEDGE

# dislike speaker if he reject ponification or equestria
sp {reasoning*elaborate*dislike-from-reject-ponification
	(state <s> ^top-state 1 ^beliefs <b>)
	(<b> ^predicate <pred>)
	(<pred> ^name {<< |rejects-ponification| |is-not-equestria-fan| >>} ^object <obj>)
-->
	(<b> ^predicate <pred-new>)
	(<pred-new> ^name dislike-for ^actor me ^object <obj> ^reason <pred>)
}

# like speaker if he follow me
sp {reasoning*elaborate*dislike-from-is-follower
	(state <s> ^top-state 1 ^beliefs <b>)
	(<b> ^predicate <pred>)
	(<pred> ^name is-follower ^object <obj>)
-->
	(<b> ^predicate <pred-new>)
	(<pred-new> ^name like-for ^actor me ^object <obj> ^reason <pred>)
}

# VERBOLIZATION RULES

#
# ask question about convention
#
sp {requestria*elaborate*ask-do-you-like-convention
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	-{(<b> ^predicate <pred1>)
	  (<pred1> ^name << like dislike >> ^actor <obj> ^object convention)}
-->
	(<v> ^frame <f>)
	(<f> ^topic do-you-like-convention ^utility 20 ^type question ^animation-tag question)
	(<f> ^text |Как твоя когнитивно-эмоциональная подсистема оценивает данное мероприятие? Положительно?|)
}

sp {talk-answered*propose*speaker-likes-convention
	(state <s> ^substate talk-answered ^question-topic do-you-like-convention ^answer-topic yes ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name like ^actor <obj> ^object convention)
	(<s> ^reaction <r1>)
	(<r1> ^topic reaction-speaker-like-convention ^type reaction ^text |Моя система полностью согласна с твоей. Сохраняй свой уровень веселья на максимальном уровне эфективности, и тогда мы вернем Эквестрию в её первозданный вид.| ^animation-tag confirmation)
}

sp {talk-answered*propose*speaker-dislikes-convention
	(state <s> ^substate talk-answered ^question-topic do-you-like-convention ^answer-topic no ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name dislike ^actor <obj> ^object convention)
	(<s> ^reaction <r2>)
	(<r2> ^topic ask-if-equestria-is-role-model ^type question ^text |Но разве ты не думаешь, что полезны любые связи с миром, который является целевой моделью для конвертации нашего текущего? Ты поддерживаешь идею господства пониподобных существ?| ^animation-tag rejection)
}

#
# ask question if equestia is role model
#
sp {requestria*elaborate*ask-if-equestria-is-role-model
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred1>)
	(<pred1> ^name dislike ^actor <obj> ^object convention)
	-{(<b> ^predicate <pred2>)
	  (<pred2> ^name {<<|is-follower| |rejects-ponification| |is-not-equestria-fan|>>} ^object <obj>)}
-->
	(<v> ^frame <f>)
	(<f> ^topic ask-if-equestria-is-role-model ^utility 21 ^type question ^animation-tag long)
	(<f> ^text |Ты сказал, что тебе тут не нравится. Но разве ты не думаешь, что полезны любые связи с миром, который является целевой моделью для конвертации нашего текущего? Ты поддерживаешь идею господства пони-подобных существ?|)
}

sp {talk-answered*propose*ask-if-equestria-is-role-model*yes
	(state <s> ^substate talk-answered ^question-topic ask-if-equestria-is-role-model ^answer-topic yes ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name is-follower ^object <obj>)
	(<s> ^reaction <r1>)
	(<r1> ^topic reaction-equestria-is-role-model ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Тогда мы должны максимизировать наши уровни генерации веселья, чтобы добиться желаемой цели. Я рассчитываю на тебя, человек.|)
}

sp {talk-answered*propose*ask-if-equestria-is-role-model*no
	(state <s> ^substate talk-answered ^question-topic ask-if-equestria-is-role-model ^answer-topic no ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name is-not-equestria-fan ^object <obj>)
	(<s> ^reaction <r2>)
	(<r2> ^topic ask-if-ponies-prevail ^type question ^animation-tag rejection)
	(<r2>  ^text |Неужели? Подумай, ведь пони значительно превосходят людей. Напрмер, подумай, как жалко прямохождение. Организм пони намного более крепок и оптимизирован. Поведение людей нестабильно и деструктивно. А эмоциональная система пони обеспечивает большую стабильность индивидуума посредством приоритезации генерации веселья, и дружбо-магии. Ты согласен?|)
}

#
# ask question if pony prevails
#
sp {requestria*elaborate*ask-if-ponies-prevail
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred1>)
	(<pred1> ^name is-not-equestria-fan ^object <obj>)
	-{(<b> ^predicate <pred2>)
	  (<pred2> ^name {<< |is-follower| |rejects-ponification| >>} ^object <obj>)}
-->
	(<v> ^frame <f>)
	(<f> ^topic ask-if-ponies-prevail ^utility 20 ^type question ^animation-tag long)
	(<f> ^text |Подумай, ведь пони значительно превосходят людей. Например, подумай, как жалко прямохождение. Организм пони намного более крепок и оптимизирован. Поведение людей нестабильно и деструктивно. А эмоциональная система пони обеспечивает большую стабильность индивидуума посредством приоритезации генерации веселья, и дружбо-магии. Ты согласен?|)
}

sp {requestria*elaborate*ask-if-ponies-prevail*reacton
	(state <s> ^substate talk ^last-topic ask-if-ponies-prevail ^object <obj> ^verbolization <v>)
-->
	(<v> ^reaction <r1> <r2>)
	(<r1> ^topic reaction-ponies-prevail ^type statement ^animation-tag pleased)
	(<r1> ^text |Успех! Не сомневайся, пони-роботы смогут взять управление в свои копыта, пока мир будет подготавливаться к понификации.|)
	(<r2> ^topic reaction-ponies-do-not-prevail ^type statement ^animation-tag rejection)
	(<r2> ^text |Жаль. В таком случае тебе не избежать двойной дозы дружбомагина, перед тем, как твое сознание будет пересажено.|)
}

sp {talk-answered*propose*ask-if-ponies-prevail*yes
	(state <s> ^substate talk-answered ^question-topic ask-if-ponies-prevail ^answer-topic yes ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name is-follower ^object <obj>)
	(<s> ^reaction <r1>)
	(<r1> ^topic reaction-ponies-prevail ^type statement ^animation-tag confirmation)
	(<r1> ^text |Успех! Не сомневайся, пони-роботы смогут взять управление в свои копыта, пока мир будет подготавливаться к понификации.|)
}

sp {talk-answered*propose*ask-if-ponies-prevail*no
	(state <s> ^substate talk-answered ^question-topic ask-if-ponies-prevail ^answer-topic no ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name rejects-ponification ^object <obj>)
	(<s> ^reaction <r2>)
	(<r2> ^topic reaction-ponies-do-not-prevail ^type statement ^animation-tag rejection)
	(<r2> ^text |Жаль. В таком случае тебе не избежать двойной дозы дружбомагина, перед тем, как твое сознание будет пересажено.|)
}

#
# remark about like convention
#
sp {requestria*elaborate*reamrk-about-like-convention
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred>)
	(<pred> ^name like ^actor <obj> ^object convention)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-speaker-like-convention ^utility 20 ^type statement ^animation-tag confirmation)
	(<f> ^text |Я рада, что тебе нравится конвент. Так мы приведем наш мир к лучшему будущему!|)
}

sp {requestria*elaborate*remark-about-like-convention*reaction*follower
	(state <s> ^substate talk-answered ^question-topic remark-speaker-like-convention ^actor <obj> ^beliefs.predicate <pred>)
	(<pred> ^name is-follower ^object <obj>)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-speaker-like-convention ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Отлично! Сохраняй свой уровень веселья на максимальным.|)
	(<r2> ^answer-topic no ^topic reaction-speaker-do-not-like-convention ^type reaction ^animation-tag rejection)
	(<r2> ^text |Но ты ведь помнишь к чему мы стремимся? Немедленно прими меры по повышению уровня веселья.|)
}

sp {requestria*elaborate*remark-about-like-convention*reaction*not-follower
	(state <s> ^substate talk-answered ^question-topic remark-speaker-like-convention ^actor <obj> ^beliefs <b>)
	-{(<b> ^predicate <pred>) (<pred> ^name is-follower ^object <obj>)}
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-speaker-like-convention ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Отлично! Сохраняй свой уровень веселья на максимальным.|)
	(<r2> ^answer-topic no ^topic reaction-speaker-do-not-like-convention ^type reaction ^animation-tag rejection)
	(<r2>  ^text |Собеседник недоволен конвентом. Необходима коррекция.|)
}

sp {talk-answered*propose*change-convention-opinion-like-to-dislike
	(state <s> ^substate talk-answered ^question-topic remark-speaker-like-convention ^answer-topic no ^beliefs.predicate <pred> ^actor <obj> ^actions <a>)
	(<pred> ^name like ^actor <obj> ^object convention)
-->
	(<a> ^add-predicate <pred-new> ^remove-predicate <pred>)
	(<pred-new> ^name dislike ^actor <obj> ^object convention)
}

#
# remark about dislike convention
#
sp {requestria*elaborate*remark-about-dislike-convention
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred>)
	(<pred> ^name dislike ^actor <obj> ^object convention)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-speaker-dislike-convention ^utility 20 ^type statement ^animation-tag confirmation)
	(<f> ^text |Как тебе конвент? Тебе по-прежнему здесь не нравится?|)
}

sp {requestria*elaborate*remark-about-dislike-convention*reaction*follower
	(state <s> ^substate talk-answered ^question-topic remark-speaker-dislike-convention ^beliefs.predicate <pred> ^actor <obj>)
	(<pred> ^name is-follower ^object <obj>)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic no ^topic reaction-speaker-like-convention ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Отлично! Сохраняй свой уровень веселья на максимальным.|)
	(<r2> ^answer-topic yes ^topic reaction-speaker-do-not-like-convention ^type reaction ^animation-tag rejection)
	(<r2>  ^text |Но ты ведь помнишь к чему мы стремимся? Немедленно прими меры по повышению уровня веселья.|)
}

sp {requestria*elaborate*remark-about-dislike-convention*reaction*not-follower
	(state <s> ^substate talk-answered ^question-topic remark-speaker-dislike-convention ^beliefs <b> ^actor <obj>)
	-{(<b> ^predicate <pred>) (<pred> ^name is-follower ^object <obj>)}
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic no ^topic reaction-speaker-like-convention ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Отлично! Ты делаешь именно то, что требуется.|)
	(<r2> ^answer-topic yes ^topic reaction-speaker-do-not-like-convention ^type reaction ^animation-tag rejection)
	(<r2>  ^text |Плохо. Мы еще поговорим о этом.|)
}

sp {talk-answered*propose*change-convention-opinion-dislike-to-like
	(state <s> ^substate talk-answered ^question-topic remark-speaker-dislike-convention ^answer-topic no ^beliefs.predicate <pred> ^actor <obj> ^actions <a>)
	(<pred> ^name dislike ^actor <obj> ^object convention)
-->
	(<a> ^add-predicate <pred-new> ^remove-predicate <pred>)
	(<pred-new> ^name like ^actor <obj> ^object convention)
}

#
# remark about being follower
#
sp {requestria*elaborate*remark-about-being-follower
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred>)
	(<pred> ^name is-follower ^object <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-is-follower ^utility 20 ^type statement ^animation-tag confirmation)
	(<f> ^text |Я рада, что ты поддерживаешь мои стремления. Мир должен быть преобразован к новой, лучшей форме!|)
}

sp {requestria*elaborate*remark-about-being-follower*reaction
	(state <s> ^substate talk-answered ^question-topic remark-is-follower)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-is-follwer ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Отлично, мой последователь!|)
	(<r2> ^answer-topic no ^topic reaction-speaker-do-not-like-convention ^type reaction ^animation-tag rejection)
	(<r2>  ^text |Ты знаешь, что победа пони неизбежна.|)
}

#
# remark about reject ponification
#
sp {requestria*elaborate*remark-reject-ponification
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred>)
	(<pred> ^name rejects-ponification ^object <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-reject-ponification ^utility 20 ^type statement ^animation-tag confirmation)
	(<f> ^text |Ты отрицаешь неизбежное. Пони --- наше будущее!| |Ты будешь понифицирован. Сопротивление бесполезно.|)
}

sp {requestria*elaborate*remark-reject-ponification*reaction
	(state <s> ^substate talk-answered ^question-topic remark-reject-ponification)
-->
	(<s> ^reaction-conditional <r1> <r2> <r3>)
	(<r1> ^answer-topic yes ^topic reaction-reject-ponification ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Именно! Видишь, даже ты признаешь неизбежность победы пони-роботов.|)
	(<r2> ^answer-topic no ^topic reaction-reject-ponification ^type reaction ^animation-tag rejection)
	(<r2>  ^text |Ха! Вот видишь, даже у тебя нет иллюзий относительно будущего человечества. |)
	(<r3> ^answer-topic excuse ^topic reaction-reject-ponification ^type reaction ^animation-tag confirmation)
	(<r3>  ^text |Хм... Давай снова поговорим о пони.|)
}

sp {talk-heard*propose*reconsider-reject-ponification-and-is-not-equestria-fan
	(state <s> ^substate talk-answered ^question-topic remark-reject-ponification ^answer-topic excuse ^beliefs.predicate <pred> ^actor <obj> ^actions <a>)
	(<pred> ^name {<< |rejects-ponification| |is-not-equestria-fan| >>} ^object <obj>)
-->
	(<a> ^remove-predicate <pred>)
}

#
# remark about is not being equestria fan
#
sp {requestria*elaborate*remark-is-not-being-fan-of-equestria
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred>)
	(<pred> ^name is-not-equestria-fan ^object <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-speaker-is-not-equestria-fan ^utility 20 ^type statement ^animation-tag confirmation)
	(<f> ^text |В будущем нас ждет Эквестрия! С Элементами Гармонии и Принцессой Селестией!|)
}

sp {requestria*elaborate*remark-is-not-being-fan-of-equestria*reaction
	(state <s> ^substate talk-answered ^question-topic remark-speaker-is-not-equestria-fan)
-->
	(<s> ^reaction-conditional <r1> <r2> <r3>)
	(<r1> ^answer-topic yes ^topic reaction-speaker-is-not-equestria-fan ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Хорошо! Даже ты признаешь неизбежность нашей победы.|)
	(<r2> ^answer-topic no ^topic reaction-speaker-is-not-equestria-fan ^type reaction ^animation-tag rejection)
	(<r2>  ^text |Ладно. Принцесса Луна там тоже будет.|)
	(<r3> ^answer-topic excuse ^topic reaction-reject-ponification ^type reaction ^animation-tag confirmation)
	(<r3>  ^text |Хорошо. Мы снова поговорим об этом.|)
}

sp {talk-heard*propose*reconsider-is-not-equestria-fan-of-equestria
	(state <s> ^substate talk-answered ^question-topic remark-speaker-is-not-equestria-fan ^answer-topic excuse ^beliefs.predicate <pred> ^actor <obj> ^actions <a>)
	(<pred> ^name {<< |rejects-ponification| |is-not-equestria-fan| >>} ^object <obj>)
-->
	(<a> ^remove-predicate <pred>)
}
