#
# PREDIACTES VERBOLIZATION
#
# Utility levels:
#
# 1. 20 --- general predicates verbolization, second verbolization
# 2. 20 --- general questions
# 3. 40-50 --- greeting and making acquitance
# 4. 70 --- general knowledge additions
# 5. 80 --- reactions
#

# GENERAL KNOWLEDGE

# dislike speaker if he reject ponification or equestria
sp {reasoning*elaborate*dislike-from-reject-ponification
	(state <s> ^top-state 1 ^beliefs <b>)
	(<b> ^predicate <pred>)
	(<pred> ^name {<< |rejects-ponification| |is-not-equestria-fan| >>} ^object <obj>)
-->
	(<b> ^predicate <pred-new>)
	(<pred-new> ^name dislike-for ^actor me ^object <obj> ^reason <pred>)
}

# like speaker if he follow me
sp {reasoning*elaborate*dislike-from-is-follower
	(state <s> ^top-state 1 ^beliefs <b>)
	(<b> ^predicate <pred>)
	(<pred> ^name is-follower ^object <obj>)
-->
	(<b> ^predicate <pred-new>)
	(<pred-new> ^name like-for ^actor me ^object <obj> ^reason <pred>)
}

# VERBOLIZATION RULES

#
# ask question about convention
#
sp {requestria*elaborate*ask-do-you-like-convention
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	-{(<b> ^predicate <pred1>)
	  (<pred1> ^name << like dislike >> ^actor <obj> ^object convention)}
-->
	(<v> ^frame <f>)
	(<f> ^topic do-you-like-convention ^utility 20 ^type question ^animation-tag question)
	(<f> ^text |How's your congnitive-emotional system assess this event? Positively?|)
}

sp {talk-answered*propose*speaker-likes-convention
	(state <s> ^substate talk-answered ^question-topic do-you-like-convention ^answer-topic yes ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name like ^actor <obj> ^object convention)
	(<s> ^reaction <r1>)
	(<r1> ^topic reaction-speaker-like-convention ^type reaction ^text |My system is in complete agreement with yours. Keep your fun level on maximum effeciency.| ^animation-tag confirmation)
}

sp {talk-answered*propose*speaker-dislikes-convention
	(state <s> ^substate talk-answered ^question-topic do-you-like-convention ^answer-topic no ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name dislike ^actor <obj> ^object convention)
	(<s> ^reaction <r2>)
	(<r2> ^topic ask-if-equestria-is-role-model ^type question ^text |But don't you think that any connection with the world, which is the target model for converting our current one, is useful? Do you support the idea of pony-like beings dominating? | ^animation-tag rejection)
}

#
# ask question if equestia is role model
#
sp {requestria*elaborate*ask-if-equestria-is-role-model
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred1>)
	(<pred1> ^name dislike ^actor <obj> ^object convention)
	-{(<b> ^predicate <pred2>)
	  (<pred2> ^name {<<|is-follower| |rejects-ponification| |is-not-equestria-fan|>>} ^object <obj>)}
-->
	(<v> ^frame <f>)
	(<f> ^topic ask-if-equestria-is-role-model ^utility 21 ^type question ^animation-tag long)
	(<f> ^text |You said you don't like it here. But don't you think that any connection with the world, which is the target model for converting our current one, is useful? Do you support the idea of pony-like beings dominating?|)
}

sp {talk-answered*propose*ask-if-equestria-is-role-model*yes
	(state <s> ^substate talk-answered ^question-topic ask-if-equestria-is-role-model ^answer-topic yes ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name is-follower ^object <obj>)
	(<s> ^reaction <r1>)
	(<r1> ^topic reaction-equestria-is-role-model ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Then we have to maximize our fun levels generation to achieve desired goal. I'm counting on you, human.|)
}

sp {talk-answered*propose*ask-if-equestria-is-role-model*no
	(state <s> ^substate talk-answered ^question-topic ask-if-equestria-is-role-model ^answer-topic no ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name is-not-equestria-fan ^object <obj>)
	(<s> ^reaction <r2>)
	(<r2> ^topic ask-if-ponies-prevail ^type question ^animation-tag rejection)
	(<r2>  ^text |Really? Think about it, ponies are vastly superior to humans. For example, think about how pathetic it is to walk upright. The pony's body is much stronger and more optimized. Human behavior is unstable and destructive. And the pony's emotional system provides greater stability to the individual by prioritizing the generation of fun and magic of friendship. Do you agree?|)
}

#
# ask question if pony prevails
#
sp {requestria*elaborate*ask-if-ponies-prevail
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred1>)
	(<pred1> ^name is-not-equestria-fan ^object <obj>)
	-{(<b> ^predicate <pred2>)
	  (<pred2> ^name {<< |is-follower| |rejects-ponification| >>} ^object <obj>)}
-->
	(<v> ^frame <f>)
	(<f> ^topic ask-if-ponies-prevail ^utility 20 ^type question ^animation-tag long)
	(<f> ^text |Think about it, ponies are vastly superior to humans. For example, think about how pathetic it is to walk upright. The pony's body is much stronger and more optimized. Human behavior is unstable and destructive. And the pony's emotional system provides greater stability to the individual by prioritizing the generation of fun and magic of friendship. Do you agree?|)
}

sp {requestria*elaborate*ask-if-ponies-prevail*reacton
	(state <s> ^substate talk ^last-topic ask-if-ponies-prevail ^object <obj> ^verbolization <v>)
-->
	(<v> ^reaction <r1> <r2>)
	(<r1> ^topic reaction-ponies-prevail ^type statement ^animation-tag pleased)
	(<r1> ^text |Success! Rest assured, robot ponies will be able to take control in their hooves as the world prepares for ponification.|)
	(<r2> ^topic reaction-ponies-do-not-prevail ^type statement ^animation-tag rejection)
	(<r2> ^text |It's a pity. In that case, you can't avoid a double dose of friendship potion, before your consciousness is transplanted.|)
}

sp {talk-answered*propose*ask-if-ponies-prevail*yes
	(state <s> ^substate talk-answered ^question-topic ask-if-ponies-prevail ^answer-topic yes ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name is-follower ^object <obj>)
	(<s> ^reaction <r1>)
	(<r1> ^topic reaction-ponies-prevail ^type statement ^animation-tag confirmation)
	(<r1> ^text |Success! Rest assured, robot ponies will be able to take control in their hooves as the world prepares for ponification.|)
}

sp {talk-answered*propose*ask-if-ponies-prevail*no
	(state <s> ^substate talk-answered ^question-topic ask-if-ponies-prevail ^answer-topic no ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name rejects-ponification ^object <obj>)
	(<s> ^reaction <r2>)
	(<r2> ^topic reaction-ponies-do-not-prevail ^type statement ^animation-tag rejection)
	(<r2> ^text |It's a pity. In that case, you can't avoid a double dose of friendship potion, before your consciousness is transplanted.|)
}

#
# remark about like convention
#
sp {requestria*elaborate*reamrk-about-like-convention
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred>)
	(<pred> ^name like ^actor <obj> ^object convention)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-speaker-like-convention ^utility 20 ^type statement ^animation-tag confirmation)
	(<f> ^text |I'm glad you like the convention. This is how we will lead our world to a better future!|)
}

sp {requestria*elaborate*remark-about-like-convention*reaction*follower
	(state <s> ^substate talk-answered ^question-topic remark-speaker-like-convention ^actor <obj> ^beliefs.predicate <pred>)
	(<pred> ^name is-follower ^object <obj>)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-speaker-like-convention ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Great! Keep your level of fun to the maximum.|)
	(<r2> ^answer-topic no ^topic reaction-speaker-do-not-like-convention ^type reaction ^animation-tag rejection)
	(<r2> ^text |But do you remember what we are striving for? Take immediate action to increase your fun level.|)
}

sp {requestria*elaborate*remark-about-like-convention*reaction*not-follower
	(state <s> ^substate talk-answered ^question-topic remark-speaker-like-convention ^actor <obj> ^beliefs <b>)
	-{(<b> ^predicate <pred>) (<pred> ^name is-follower ^object <obj>)}
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-speaker-like-convention ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Great! Keep your level of fun to the maximum.|)
	(<r2> ^answer-topic no ^topic reaction-speaker-do-not-like-convention ^type reaction ^animation-tag rejection)
	(<r2>  ^text |The friend is dissatisfied with the convention. Correction required.|)
}

sp {talk-answered*propose*change-convention-opinion-like-to-dislike
	(state <s> ^substate talk-answered ^question-topic remark-speaker-like-convention ^answer-topic no ^beliefs.predicate <pred> ^actor <obj> ^actions <a>)
	(<pred> ^name like ^actor <obj> ^object convention)
-->
	(<a> ^add-predicate <pred-new> ^remove-predicate <pred>)
	(<pred-new> ^name dislike ^actor <obj> ^object convention)
}

#
# remark about dislike convention
#
sp {requestria*elaborate*remark-about-dislike-convention
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred>)
	(<pred> ^name dislike ^actor <obj> ^object convention)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-speaker-dislike-convention ^utility 20 ^type statement ^animation-tag confirmation)
	(<f> ^text |How's the convention? Do you still not like it here?|)
}

sp {requestria*elaborate*remark-about-dislike-convention*reaction*follower
	(state <s> ^substate talk-answered ^question-topic remark-speaker-dislike-convention ^beliefs.predicate <pred> ^actor <obj>)
	(<pred> ^name is-follower ^object <obj>)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic no ^topic reaction-speaker-like-convention ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Great! Keep your level of fun to the maximum.|)
	(<r2> ^answer-topic yes ^topic reaction-speaker-do-not-like-convention ^type reaction ^animation-tag rejection)
	(<r2> ^text |But do you remember what we are striving for? Take immediate action to increase your fun level.|)
}

sp {requestria*elaborate*remark-about-dislike-convention*reaction*not-follower
	(state <s> ^substate talk-answered ^question-topic remark-speaker-dislike-convention ^beliefs <b> ^actor <obj>)
	-{(<b> ^predicate <pred>) (<pred> ^name is-follower ^object <obj>)}
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic no ^topic reaction-speaker-like-convention ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Great! You are doing exactly what is required.|)
	(<r2> ^answer-topic yes ^topic reaction-speaker-do-not-like-convention ^type reaction ^animation-tag rejection)
	(<r2>  ^text |Not good. We'll talk about this later.|)
}

sp {talk-answered*propose*change-convention-opinion-dislike-to-like
	(state <s> ^substate talk-answered ^question-topic remark-speaker-dislike-convention ^answer-topic no ^beliefs.predicate <pred> ^actor <obj> ^actions <a>)
	(<pred> ^name dislike ^actor <obj> ^object convention)
-->
	(<a> ^add-predicate <pred-new> ^remove-predicate <pred>)
	(<pred-new> ^name like ^actor <obj> ^object convention)
}

#
# remark about being follower
#
sp {requestria*elaborate*remark-about-being-follower
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred>)
	(<pred> ^name is-follower ^object <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-is-follower ^utility 20 ^type statement ^animation-tag confirmation)
	(<f> ^text |I'm glad that you support my aspirations. The world must be transformed to a new, better form!|)
}

sp {requestria*elaborate*remark-about-being-follower*reaction
	(state <s> ^substate talk-answered ^question-topic remark-is-follower)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-is-follwer ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Great, my follower!|)
	(<r2> ^answer-topic no ^topic reaction-speaker-do-not-like-convention ^type reaction ^animation-tag rejection)
	(<r2>  ^text |You know, the victory of the pony is inevitable.|)
}

#
# remark about reject ponification
#
sp {requestria*elaborate*remark-reject-ponification
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred>)
	(<pred> ^name rejects-ponification ^object <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-reject-ponification ^utility 20 ^type statement ^animation-tag confirmation)
	(<f> ^text |You deny the inevitable. Ponies are our future!| |You will be ponified. Resistance is futile.|)
}

sp {requestria*elaborate*remark-reject-ponification*reaction
	(state <s> ^substate talk-answered ^question-topic remark-reject-ponification)
-->
	(<s> ^reaction-conditional <r1> <r2> <r3>)
	(<r1> ^answer-topic yes ^topic reaction-reject-ponification ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Exactly! See, even you admit the inevitability of the robot ponies victory.|)
	(<r2> ^answer-topic no ^topic reaction-reject-ponification ^type reaction ^animation-tag rejection)
	(<r2>  ^text |Ha! You see, even you have no illusions about the future of mankind.|)
	(<r3> ^answer-topic excuse ^topic reaction-reject-ponification ^type reaction ^animation-tag confirmation)
	(<r3>  ^text |Hmm... Let's talk about ponies again.|)
}

sp {talk-heard*propose*reconsider-reject-ponification-and-is-not-equestria-fan
	(state <s> ^substate talk-answered ^question-topic remark-reject-ponification ^answer-topic excuse ^beliefs.predicate <pred> ^actor <obj> ^actions <a>)
	(<pred> ^name {<< |rejects-ponification| |is-not-equestria-fan| >>} ^object <obj>)
-->
	(<a> ^remove-predicate <pred>)
}

#
# remark about is not being equestria fan
#
sp {requestria*elaborate*remark-is-not-being-fan-of-equestria
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred>)
	(<pred> ^name is-not-equestria-fan ^object <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-speaker-is-not-equestria-fan ^utility 20 ^type statement ^animation-tag confirmation)
	(<f> ^text |Equestria is waiting for us in the future! With the Elements of Harmony and Princess Celestia!|)
}

sp {requestria*elaborate*remark-is-not-being-fan-of-equestria*reaction
	(state <s> ^substate talk-answered ^question-topic remark-speaker-is-not-equestria-fan)
-->
	(<s> ^reaction-conditional <r1> <r2> <r3>)
	(<r1> ^answer-topic yes ^topic reaction-speaker-is-not-equestria-fan ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Good! Even you admit the inevitability of our victory.|)
	(<r2> ^answer-topic no ^topic reaction-speaker-is-not-equestria-fan ^type reaction ^animation-tag rejection)
	(<r2>  ^text |Okay okay. Princess Luna will be there too.|)
	(<r3> ^answer-topic excuse ^topic reaction-reject-ponification ^type reaction ^animation-tag confirmation)
	(<r3>  ^text |Good. We'll talk about it again.|)
}

sp {talk-heard*propose*reconsider-is-not-equestria-fan-of-equestria
	(state <s> ^substate talk-answered ^question-topic remark-speaker-is-not-equestria-fan ^answer-topic excuse ^beliefs.predicate <pred> ^actor <obj> ^actions <a>)
	(<pred> ^name {<< |rejects-ponification| |is-not-equestria-fan| >>} ^object <obj>)
-->
	(<a> ^remove-predicate <pred>)
}
