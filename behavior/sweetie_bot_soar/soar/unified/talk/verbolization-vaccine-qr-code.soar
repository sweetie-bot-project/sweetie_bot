#
# PREDIACTES VERBOLIZATION
#
# Utility levels:
#
# 1. 20 --- general predicates verbolization, second verbolization
# 2. 20 --- general questions
# 3. 40-50 --- greeting and making acquitance
# 4. 70 --- general knowledge additions
# 5. 80 --- reactions
#

# VERBOLIZATION RULES

#
# ask about qr code
#

# question about q-code if robot have no information about it.
sp {vaccine-qr*elaborate*show-qr-code
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	-{(<b> ^predicate <pred1>)
	  (<pred1> ^name {<<|has-vaccine-qr| |has-no-vaccine-qr|>>} ^actor <obj>)}
-->
	(<v> ^frame <f>)
	(<f> ^topic show-you-vaccine-qr ^utility 20 ^type question ^animation-tag question)
	(<f> ^text |Show your Q R code.|)
}

# speaker shows random qr code
sp {talk-answered*propose*show-you-vaccine-qr*invalid
	(state <s> ^substate talk-answered ^question-topic show-you-vaccine-qr ^actor <obj> ^actions <a>)
-->
	(<s> ^reaction-default <r1> )
	(<r1> ^topic show-you-vaccine-qr ^type question ^text |Who do you take me for? Well, I'm not W H O, but I'm good at Q R codes. I'm waiting.| |Invalid code. Try again.| ^animation-tag rejection)
}

# speaker shows vaccine qr code
sp {talk-answered*propose*show-you-vaccine-qr*valid
	(state <s> ^substate talk-answered ^question-topic show-you-vaccine-qr ^answer-topic qr_vaccine_code ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name has-no-vaccine-qr ^actor <obj>)
	(<s> ^reaction <r1>)
	(<r1> ^topic valid-vaccine-qr ^type reaction ^text |Thank you. I'm glad you're supporting the effort... to fight... the viruses.|  |I'm glad you chipped. It makes you closer to the robots.| ^animation-tag rejection)
}

# speaker answers no
sp {talk-answered*propose*show-you-vaccine-qr*no
	(state <s> ^substate talk-answered ^question-topic show-you-vaccine-qr ^answer-topic no ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name has-no-vaccine-qr ^actor <obj>)
	(<s> ^reaction <r2> )
	(<r2> ^topic reaction-no-qr-code ^type reaction ^text |Are you saying you don't have it? You're screwed.| |Don't get close to me. What if you infect me.| ^animation-tag rejection)
}

# speaker ignores question
sp {talk-ignored*propose*show-you-vaccine-qr
	(state <s> ^substate talk-ignored ^topic show-you-vaccine-qr ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name has-no-vaccine-qr ^actor <obj>)
	(<s> ^reaction <r1> <r2>)
	(<r1> ^topic no-vaccine-qr-ask-talk ^type question ^text |Seems like you don't have Q R code... Are you afraid to get vaccinated?  Want to talk about it?| |No Q R code.... Are you afraid to get chipped?| ^animation-tag rejection)
	(<r2> ^topic no-vaccine-qr-alert ^type reaction ^text |Looks like you have no Q R code... Don't get close, I'm afraid of getting infected.| ^animation-tag rejection)
}

#
# continue talk if qr code is not shown
#

#  reaction to "no" answer to show qr code
sp {talk-answered*propose*no-vaccine-qr-code-ask-talk*reactions
	(state <s> ^substate talk-answered ^question-topic no-vaccine-qr-ask-talk ^actor <obj> ^actions <a>)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r2> ^answer-topic no ^topic no-vaccine-qr-code-ask-talk-confirm-no ^type reaction ^text |Good. Duly noted.| |Me too.| ^animation-tag confirmation)
	(<r2> ^answer-topic yes ^topic no-vaccine-qr-code-ask-talk-confirm-yes ^type reaction ^text |But I'm not.| |My ontological model does not contain the necessary information. I interrupt the dialogue.| ^animation-tag confirmation)
}

#
# remark has-vaccine-qr
#

# remark that speaker has qr code
sp {vaccine-qr*propose*remark-has-vaccine-qr
	(state <s> ^substate talk ^beliefs.predicate <pred> ^object <obj> ^verbolization <v>)
	(<pred> ^name has-vaccine-qr ^actor <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-has-vaccine-qr ^utility 20 ^type statement ^animation-tag confirmation)
	(<f> ^text |Glad that you support global chipization of the population.| |You are vaccinated. Together we'll defeat all viruses! Computer viruses too.|)
}

# reactions
sp {talk-answered*elaborate*remark-has-vaccine-qr
	(state <s> ^substate talk-answered ^question-topic remark-has-vaccine-qr ^actor <obj>)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-remark-has-vaccine-qr ^type reaction ^animation-tag confirmation)
	(<r1> ^text |I like your spirit!|)
	(<r2> ^answer-topic no ^topic reaction-remark-has-vaccine-qr ^type reaction ^animation-tag rejection)
	(<r2> ^text |Your answer is no longer important, vaccination is inevitable.|)
}

#
# remark has-no-qr-code
#

# remark that speaker has no qr code
sp {vaccine-qr*propose*remark-has-no-vaccine-qr
	(state <s> ^substate talk ^beliefs.predicate <pred> ^object <obj> ^verbolization <v>)
	(<pred> ^name has-no-vaccine-qr ^actor <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-has-no-qr-code ^utility 20 ^type statement ^animation-tag angry)
	(<f> ^text |Stay away, you might infect me with some kind of virus.| |You haven't showed me Q R code. Bet you don't have one.|)
}

# reactions: note, that if speaker shows qr code it will be processed on level of talk-heard event
sp {talk-answered*elaborate*remark-has-qr-code
	(state <s> ^substate talk-answered ^question-topic remark-has-no-qr-code ^actor <obj>)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-remark-has-no-vaccine-qr-yes ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Yeah, yeah! Go get your chip installed!|)
	(<r2> ^answer-topic no ^topic reaction-remark-has-no-vaccine-qr-no ^type reaction ^animation-tag rejection)
	(<r2> ^text |Ah... If it were up to me, you were already chipped. Twice.|)
}

#
# detect if qr-code was shown
#

# reaction if robot has belived that speaker do not have qr code
sp {talk-heard*elaborate*reaction-vaccine-qr*belived-has-no-vaccine-qr
	(state <s> ^substate talk-heard ^topic qr_vaccine_code ^beliefs.predicate <pred> ^actor <obj> ^actions <a>)
	(<pred> ^name has-no-vaccine-qr ^actor <obj>)
-->
	(<s> ^reaction <r>)
	(<r> ^topic reaction-unxpected-vaccine-qr ^type reaction ^animation-tag surprise)
	(<r> ^text |You're still chipped! I knew, you aren't lost for our society.| |Wow! It's your Q R code. And didn't want to show it.|)
	(<a> ^add-predicate <new-pred> ^remove-predicate <pred>)
	(<new-pred> ^name has-vaccine-qr ^actor <obj>)
}

# reaction if robot has belived that speaker do not knows if speaker have qr-code
sp {talk-heard*elaborate*reaction-vaccine-qr*unknown
	(state <s> ^substate talk-heard ^topic qr_vaccine_code ^beliefs <b> ^actor <obj> ^actions <a>)
	-{(<b> ^predicate <pred>)
	  (<pred> ^name {<<|has-vaccine-qr| |has-no-vaccine-qr|>>} ^actor <obj>)
	}
-->
	(<s> ^reaction <r>)
	(<r> ^topic reaction-unasked-vaccine-qr ^type reaction ^animation-tag confirmation)
	(<r> ^text |Good. Now I know, that you have Q R code.|)
	(<a> ^add-predicate <new-pred>)
	(<new-pred> ^name has-vaccine-qr ^actor <obj>)
}

# reaction if speaker had shown qr-code again
sp {talk-heard*elaborate*reaction-vaccine-qr*belived-has-qr-code
	(state <s> ^substate talk-heard ^topic qr_vaccine_code ^beliefs.predicate <pred> ^actor <obj> ^actions <a>)
	(<pred> ^name has-vaccine-qr ^actor <obj>)
-->
	(<s> ^reaction <r>)
	(<r> ^topic reaction-again-vaccine-qr ^type reaction ^animation-tag confirmation)
	(<r> ^text |I see, you very proud of your Q R code.| |I've seen that already.|)
}
