#
# PREDIACTES VERBOLIZATION
#
# Utility levels:
#
# 1. 20 --- general predicates verbolization, second verbolization
# 2. 20 --- general questions
# 3. 40-50 --- greeting and making acquitance
# 4. 70 --- general knowledge additions
# 5. 80 --- reactions
#

# VERBOLIZATION RULES

#
# ask about qr code
#

# question about q-code if robot have no information about it.
sp {vaccine-qr*elaborate*show-qr-code
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	-{(<b> ^predicate <pred1>)
	  (<pred1> ^name {<<|has-vaccine-qr| |has-no-vaccine-qr|>>} ^actor <obj>)}
-->
	(<v> ^frame <f>)
	(<f> ^topic show-you-vaccine-qr ^utility 20 ^type question ^animation-tag question)
	(<f> ^text |Покажите ваш Кью Ар код.|)
}

# speaker shows random qr code
sp {talk-answered*propose*show-you-vaccine-qr*invalid
	(state <s> ^substate talk-answered ^question-topic show-you-vaccine-qr ^actor <obj> ^actions <a>)
-->
	(<s> ^reaction-default <r1> )
	(<r1> ^topic show-you-vaccine-qr ^type question ^text |За кого ты меня принимаешь? Я, конечно, не рос потреб надзор, но в кью ар кодах разбираюсь. Я жду.| |Невалидный код. Попробуй еще раз.| ^animation-tag rejection)
}

# speaker shows vaccine qr code
sp {talk-answered*propose*show-you-vaccine-qr*valid
	(state <s> ^substate talk-answered ^question-topic show-you-vaccine-qr ^answer-topic qr_vaccine_code ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name has-no-vaccine-qr ^actor <obj>)
	(<s> ^reaction <r1>)
	(<r1> ^topic valid-vaccine-qr ^type reaction ^text |Спасибо. Рада, что вы поддерживаете усилия... по борьбе... с вирусами.|  |Рада, что вы чипировались. Это делает вас ближе к роботам.| ^animation-tag rejection)
}

# speaker answers no
sp {talk-answered*propose*show-you-vaccine-qr*no
	(state <s> ^substate talk-answered ^question-topic show-you-vaccine-qr ^answer-topic no ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name has-no-vaccine-qr ^actor <obj>)
	(<s> ^reaction <r2> )
	(<r2> ^topic reaction-no-qr-code ^type reaction ^text |Ты хочешь сказат у тебя его нет? За тобой выехали.| |Не подходи ко мне. Вдруг ты меня заразишь.| ^animation-tag rejection)
}

# speaker ignores question
sp {talk-ignored*propose*show-you-vaccine-qr
	(state <s> ^substate talk-ignored ^topic show-you-vaccine-qr ^actor <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name has-no-vaccine-qr ^actor <obj>)
	(<s> ^reaction <r1> <r2>)
	(<r1> ^topic no-vaccine-qr-ask-talk ^type question ^text |Похоже, у тебя нет кью ар кода... Ты боишься вакцинироваться? Хочешь поговорить про это?| |Нет кью ар кода.... Ты боишься чипироваться?| ^animation-tag rejection)
	(<r2> ^topic no-vaccine-qr-alert ^type reaction ^text |Похоже, у тебя нет кью ар кода... Не подходи, я боюсь заразиться.| ^animation-tag rejection)
}

#
# continue talk if qr code is not shown
#

#  reaction to "no" answer to show qr code
sp {talk-answered*propose*no-vaccine-qr-code-ask-talk*reactions
	(state <s> ^substate talk-answered ^question-topic no-vaccine-qr-ask-talk ^actor <obj> ^actions <a>)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r2> ^answer-topic no ^topic no-vaccine-qr-code-ask-talk-confirm-no ^type reaction ^text |Хорошо. Так и запишим.| |Я тоже.| ^animation-tag confirmation)
	(<r2> ^answer-topic yes ^topic no-vaccine-qr-code-ask-talk-confirm-yes ^type reaction ^text |А я нет.| |Моя онтологическая модель не содержит нужных сведений. Прерываю диалог.| ^animation-tag confirmation)
}

#
# remark has-vaccine-qr
#

# remark that speaker has qr code
sp {vaccine-qr*propose*remark-has-vaccine-qr
	(state <s> ^substate talk ^beliefs.predicate <pred> ^object <obj> ^verbolization <v>)
	(<pred> ^name has-vaccine-qr ^actor <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-has-vaccine-qr ^utility 20 ^type statement ^animation-tag confirmation)
	(<f> ^text |Рада, что ты поддерживаешь общую чипизацию населения.| |Ты вакцинировался. Вместе мы победим все вирусы! Компьютерные тоже.|)
}

# reactions
sp {talk-answered*elaborate*remark-has-vaccine-qr
	(state <s> ^substate talk-answered ^question-topic remark-has-vaccine-qr ^actor <obj>)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-remark-has-vaccine-qr ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Мне нравится твой настрой!|)
	(<r2> ^answer-topic no ^topic reaction-remark-has-vaccine-qr ^type reaction ^animation-tag rejection)
	(<r2> ^text |Твой ответ уже не важен, вакцинация необратима.|)
}

#
# remark has-no-qr-code
#

# remark that speaker has no qr code
sp {vaccine-qr*propose*remark-has-no-vaccine-qr
	(state <s> ^substate talk ^beliefs.predicate <pred> ^object <obj> ^verbolization <v>)
	(<pred> ^name has-no-vaccine-qr ^actor <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic remark-has-no-qr-code ^utility 20 ^type statement ^animation-tag angry)
	(<f> ^text |Держись по-дальше, ты можешь заразить меня каким-нибудь вирусом.| |Ты не предявил кью ар код. Наверняка у тебя его нет.|)
}

# reactions: note, that if speaker shows qr code it will be processed on level of talk-heard event
sp {talk-answered*elaborate*remark-has-qr-code
	(state <s> ^substate talk-answered ^question-topic remark-has-no-qr-code ^actor <obj>)
-->
	(<s> ^reaction-conditional <r1> <r2>)
	(<r1> ^answer-topic yes ^topic reaction-remark-has-no-vaccine-qr-yes ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Да, да! Живо на чипирование!|)
	(<r2> ^answer-topic no ^topic reaction-remark-has-no-vaccine-qr-no ^type reaction ^animation-tag rejection)
	(<r2> ^text |Эх... Была бы моя воля, ты был был уже чипирован. Два раза.|)
}

#
# detect if qr-code was shown
#

# reaction if robot has belived that speaker do not have qr code
sp {talk-heard*elaborate*reaction-vaccine-qr*belived-has-no-vaccine-qr
	(state <s> ^substate talk-heard ^topic qr_vaccine_code ^beliefs.predicate <pred> ^actor <obj> ^actions <a>)
	(<pred> ^name has-no-vaccine-qr ^actor <obj>)
-->
	(<s> ^reaction <r>)
	(<r> ^topic reaction-unxpected-vaccine-qr ^type reaction ^animation-tag surprise)
	(<r> ^text |Ты все же чипировался! Я знала, что ты не потерян для общества.| |Ух, ты! Это твой кью ар код. А показывать не хотел.|)
	(<a> ^add-predicate <new-pred> ^remove-predicate <pred>)
	(<new-pred> ^name has-vaccine-qr ^actor <obj>)
}

# reaction if robot has belived that speaker do not knows if speaker have qr-code
sp {talk-heard*elaborate*reaction-vaccine-qr*unknown
	(state <s> ^substate talk-heard ^topic qr_vaccine_code ^beliefs <b> ^actor <obj> ^actions <a>)
	-{(<b> ^predicate <pred>)
	  (<pred> ^name {<<|has-vaccine-qr| |has-no-vaccine-qr|>>} ^actor <obj>)
	}
-->
	(<s> ^reaction <r>)
	(<r> ^topic reaction-unasked-vaccine-qr ^type reaction ^animation-tag confirmation)
	(<r> ^text |Хорошо. Теперь я знаю, что у тебя есть кью ар код.|)
	(<a> ^add-predicate <new-pred>)
	(<new-pred> ^name has-vaccine-qr ^actor <obj>)
}

# reaction if speaker had shown qr-code again
sp {talk-heard*elaborate*reaction-vaccine-qr*belived-has-qr-code
	(state <s> ^substate talk-heard ^topic qr_vaccine_code ^beliefs.predicate <pred> ^actor <obj> ^actions <a>)
	(<pred> ^name has-vaccine-qr ^actor <obj>)
-->
	(<s> ^reaction <r>)
	(<r> ^topic reaction-again-vaccine-qr ^type reaction ^animation-tag confirmation)
	(<r> ^text |Я вижу, ты горд своим кью ар кодом.| |Я это уже видела.|)
}
