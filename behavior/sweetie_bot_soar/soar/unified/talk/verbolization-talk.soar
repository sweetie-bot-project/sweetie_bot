#
# PREDIACTES VERBOLIZATION
#
# Perform verblization of talk-related predicates
#
# * (<pred> ^name possible-talk-topic ^object <obj> ^topic <a-topic> ^previous-topic <q-topic> ^reaction talk-answered ^deadline-time 60.0) --- possible reaction to 
# *	(<pred> ^name ignor-possibility ^actor <obj> ^topic <topic> ^deadline-time 60.0) --- question is possibly being ignored.
# *	(<pred> ^name ignor-topic ^actor <obj> ^topic <topic> ^deadline-time 600.0) --- question on specific topic is being ignored.
# *	(<pred> ^name ignor-me ^actor <obj>) --- speaker ignores me.
# *	(<pred> ^name nonsense-possibility ^actor <obj> ^topic <topic> ^deadline-time 60.0) --- answer has no meaning
# *	(<pred> ^name nonsense-me ^actor <obj>) --- speaker doe not interact normally
#
# Utility levels:
#
# 1. 20 --- general predicates verbolization, second verbolization
# 2. 20 --- general questions
# 3. 40-50 --- greeting and making acquitance
# 4. 70 --- general knowledge additions
# 5. 80 --- reactions
#

# GENERAL KNOWLEDGE

# maybe we are not ignored but speaker do not know how to talk
sp {verbolization*elaborate*maybe-do-not-know-how-to-talk
	(state <s> ^top-state 1 ^beliefs <b>)
	(<b> ^predicate <pred1>)
	(<pred1> ^name {<<|ignor-possibility| |nonsense-possibility|>>} ^actor <obj>)
	-{(<b> ^predicate <pred2>)
	  (<pred2> ^name explained-how-to-talk ^object <obj>)}
-->
	(<b> ^predicate <pred>)
	(<pred> ^name maybe-do-not-know-how-to-talk ^object <obj>)
}

sp {talk-answered*propose*remove*maybe-do-not-know-how-to-talk
	(state <s> ^substate talk-answered ^beliefs.predicate <pred> ^object <obj> ^actions <a>)
	(<pred> ^name maybe-do-not-know-how-to-talk ^object <obj>)
-->
	(<a> ^remove-predicate <pred>)
}

# VERBOLIZATION RULES

# decode possible reaction into verbolization frame

# reaction without utility
sp {verbolization*elaborate*talk-possible-reaction*no-utility
	(state <s> ^substate talk ^beliefs.predicate <pred> ^object <obj> ^verbolization <v>)
	(<pred> ^name talk-possible-reaction ^object <obj> ^reaction <r>)
	(<r> ^topic <topic> ^text <text> ^type <type> ^animation-tag <tag> -^utility)
-->
	(<v> ^frame <f>)
	(<f> ^topic <topic> ^utility 60 ^type <type> ^animation-tag <tag> ^text <text>)
}

# reaction with utility
sp {verbolization*elaborate*talk-possible-reaction*utiity
	(state <s> ^substate talk ^beliefs.predicate <pred> ^object <obj> ^verbolization <v>)
	(<pred> ^name talk-possible-reaction ^object <obj> ^reaction <r>)
	(<r> ^topic <topic> ^text <text> ^type <type> ^animation-tag <tag> ^utility <utility>)
-->
	(<v> ^frame <f>)
	(<f> ^topic <topic> ^utility <utility> ^type <type> ^animation-tag <tag> ^text <text>)
}

# remove possible-reaction predicates
sp {talk-said*propose*remove-possible-reaction
	(state <s> ^substate talk-said ^object <obj> ^topic <topic> ^beliefs.predicate <pred> ^actions <a>)
	(<pred> ^name talk-possible-reaction ^object <obj> ^reaction.topic <topic>)
-->
	(<a> ^remove-predicate <pred>)
}

#
# explain how to talk to speaker
#

# topic propose rule
sp {verbolization*elaborate*explanation-how-to-talk
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred1>)
	(<pred1> ^name maybe-do-not-know-how-to-talk ^object <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic explanation-how-to-talk ^type question ^utility 65 ^animation-tag long)
	(<f> ^text |Can you see cards in front of me? To give an answer, take one of those and show me. Answer me with a card, please!|)
}

# verbal reactions
sp {talk-answered*propose*explained-how-to-talk-reaction-yes
	(state <s> ^substate talk-answered ^question-topic explanation-how-to-talk ^answer-topic yes)
-->
	(<s> ^reaction <r1>)
	(<r1> ^topic reaction-encouraged ^type reaction ^animation-tag confirmation)
	(<r1> ^text |Well done!| |Great!|)
}

sp {talk-answered*propose*explained-how-to-talk-reaction-default
	(state <s> ^substate talk-answered ^question-topic explanation-how-to-talk)
-->
	(<s> ^reaction-default <r2>)
	(<r2> ^topic reaction-puzzled ^type reaction ^animation-tag surprise)
	(<r2> ^text |I've expected something different, but it'll do too.| |Good. Let's assume, you know how to use cards now.|)
}

# remember that explanation is performed
sp {talk-answered*propose*explained-how-to-talk
	(state <s> ^substate talk-said ^topic explanation-how-to-talk ^object <obj> ^actions <a>)
-->
	(<a> ^add-predicate <pred>)
	(<pred> ^name explained-how-to-talk ^object <obj>)
}

# assume that ignorance was caused by lack of knoweledge
sp {talk-answered*propose*explained-how-to-talk*remove-ignor-predicates
	(state <s> ^substate talk-answered ^question-topic explanation-how-to-talk ^actor <obj> ^beliefs.predicate <pred> ^actions <a>)
	(<pred> ^name {<< |ignor-possibility| |nonsense-possibility| >>} ^actor <obj>)
-->
	(<a> ^remove-predicate <pred>)
}

#
# make remark about ignorance
#

sp {verbolization*elaborate*remark-about-ignorance
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred1>)
	(<pred1> ^name ignor-possibility ^actor <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic ignorance-remark ^utility 60 ^type reaction ^animation-tag notification)
	(<f> ^text |Can you hear me? I'm talking to you!| |Do not ignore me.|)
}

sp {talk-said*propose*said-about-ignorance
	(state <s> ^substate talk-said ^topic ignorance-remark ^object <obj> ^actions <a> ^beliefs.predicate <pred>)	
	(<pred> ^name ignor-possibility ^actor <obj>)
-->
	(<a> ^remove-predicate <pred>)
}

#
# make remark about ignored topic: it is actual only if matches to last topic
#
sp {verbolization*elaborate*remark-about-ignor-topic
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^last-said-topic <topic> ^verbolization <v>)
	(<b> ^predicate <pred1>)
	(<pred1> ^name ignor-topic ^actor <obj> ^topic <topic>)
-->
	(<v> ^frame <f>)
	(<f> ^topic ignor-topic-remark ^utility 60 ^type reaction ^animation-tag notification)
	(<f> ^text |I guess, you don't like this topic.| |Okay, I'll try to ask something different.|)
}

#
# make remark about systematic ignorance
#

# on addition of this event
sp {verbolization*elaborate*remark-about-ignor-me-addition
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v> ^time <clock>)
	(<b> ^predicate <pred>)
	(<pred> ^name ignor-me ^actor <obj> ^initiated-at <time>)
	-(<clock> ^just {>= <time>})
-->
	(<v> ^frame <f>)
	(<f> ^topic ignor-me-remark ^utility 70 ^type statement ^animation-tag rejection)
	(<f> ^text |You're ignoring me!| |You're not answering! It's impossible to talk to you!|)
}

# if systematic ignorance was detectedin past
sp {verbolization*elaborate*remark-about-ignor-me
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred>)
	(<pred> ^name ignor-me ^actor <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic ignor-me-remark ^utility 20 ^type statement ^animation-tag rejection)
	(<f> ^text |I remember, how you ignored me and didn't apologize!|)
}

# reaction
sp {talk-answered*elaborate*ignor-me-excused
	(state <s> ^substate talk-answered ^question-topic ignor-me-remark ^answer-topic sorry ^beliefs <b> ^object <obj> ^actions <a>)
	(<b> ^predicate <pred>)
	(<pred> ^name ignor-me ^actor <obj>)
-->
	(<s> ^reaction <r>)
	(<r> ^topic ignor-me-excused ^type reaction ^animation-tag confirmation)
	(<r> ^text |Apology accepted!|)
	(<a> ^remove-predicate <pred>)
}

#	
# make remark about nonsense
#

sp {verbolization*elaborate*remark-about-nonsense
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred1>)
	(<pred1> ^name nonsense-possibility ^actor <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic nonsense-remark ^utility 60 ^type reaction ^animation-tag surprise)
	(<f> ^text |And how to interpret that? Don't wave cards at me with no reason.| |I don't understand you. Wait until I ask a question, and then answer.| |Just show me a answer card, after I ask a question.|)
}

sp {talk-said*propose*said-about-nonsense
	(state <s> ^substate talk-said ^topic nonsense-remark ^object <obj> ^actions <a> ^beliefs.predicate <pred>)	
	(<pred> ^name nonsense-possibility ^actor <obj>)
-->
	(<a> ^remove-predicate <pred>)
}

#
# make remark about interruption
#

sp {verbolization*elaborate*remark-about-interruption
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v>)
	(<b> ^predicate <pred1>)
	(<pred1> ^name interrupted-me ^actor <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic interrupt-remark ^utility 60 ^type reaction ^animation-tag notification)
	(<f> ^text |Please, don't interrupt me! Show the card when I'm expecting an answer.| |Show the card, when I stop talking, and when I saw it --- take it away.|)
}

sp {talk-said*propose*said-about-interruption
	(state <s> ^substate talk-said ^topic interrupt-remark ^object <obj> ^actions <a> ^beliefs.predicate <pred>)	
	(<pred> ^name interrupted-me ^actor <obj>)
-->
	(<a> ^remove-predicate <pred>)
}

#
# make remark about systematic nonsense
#

# on predicate addition
sp {verbolization*elaborate*remark-about-nonsense-me-addition 
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v> ^time <clock>)
	(<b> ^predicate <pred>)
	(<pred> ^name nonsense-me ^actor <obj> ^initiated-at <time>)
	-(<clock> ^just {>= <time>})
-->
	(<v> ^frame <f>)
	(<f> ^topic nonsense-me-remark ^utility 60 ^type statement ^animation-tag anger)
	(<f> ^text |Stop talking nonsense!| |Enough! My head hurts from your cards!|)
}

# general verbolization
sp {verbolization*elaborate*remark-about-nonsense-me
	(state <s> ^substate talk ^beliefs <b> ^object <obj> ^verbolization <v> ^time <clock>)
	(<b> ^predicate <pred>)
	(<pred> ^name nonsense-me ^actor <obj> ^initiated-at <time>)
-->
	(<v> ^frame <f>)
	(<f> ^topic nonsense-me-remark ^utility 20 ^type statement ^animation-tag rejection)
	(<f> ^text |You don't know how to use cards with phrases. I don't know what to do with you.| |You should just show the answer card when I ask.|)
}

# reaction
sp {talk-answered*elaborate*nonsense-me-excused
	(state <s> ^substate talk-answered ^question-topic nonsense-me-remark ^answer-topic sorry ^beliefs <b> ^object <obj> ^actions <a>)
	(<b> ^predicate <pred>)
	(<pred> ^name nonsense-me ^actor <obj>)
-->
	(<s> ^reaction <r>)
	(<r> ^topic nonsense-me-excused ^type reaction ^animation-tag confirmation)
	(<r> ^text |Apology accepted!|)
	(<a> ^remove-predicate <pred>)
}

# detect that speaker say nonsense no more
sp {talk-answered*propose*terminate-nonsense-me
	(state <s> ^substate talk-answered ^conclusion {<<|answer-specific-reaction| |answer-general-reaction|>>} ^event <ev1> ^actor <obj> ^beliefs <b> ^actions <a>)
	(<b> ^predicate <pred>)
	(<pred> ^name nonsense-me ^actor <obj>)
	(<s> ^talk-answered <ev-answered> ^talk-irrelevant <ev-irrelevant>)
	(<ev-answered> ^event {<> <ev1>})
    (<ev-irrelevant> -^event)
-->
	(<a> ^remove-predicate <pred>)	
	(<a> ^add-predicate <pred-reaction>)
	(<s> ^reaction <r>)
	(<r> ^topic nonsense-me-terminated ^type reaction ^animation-tag surprise)
	(<r> ^text |Look how's learned how to use cards! Not bad!| |Now you know how to use the cards. People are trainable!.|)
}

#
# missing speaker
#
sp {verbolization*elaborate*missing-speaker
	(state <s> ^substate talk ^object <obj> ^beliefs.predicate <pred> ^verbolization <v>)
	(<pred> ^name missing-speaker ^object <obj>)
-->
	(<v> ^frame <f>)
	(<f> ^topic missing-speaker ^type reaction ^text |Leaving during a conversation is impolite.|)
}

sp {verbolization*elaborate*missing-speaker*acquitance
	(state <s> ^substate talk ^object <obj> ^beliefs.predicate <pred> ^verbolization.frame <f>)
	(<f> ^topic missing-speaker)
	(<pred> ^name acquitance ^object <obj>)
	(<obj> ^label <label>)
-->
	(<f> ^text (concat |Where are you? Where have you gone, friend? | <label> |?|))
}

sp {verbolization*elaborate*missing-speaker*not-greeted
	(state <s> ^substate talk ^object <obj> ^process <proc> ^verbolization.frame <f>)
	(<f> ^topic missing-speaker)
	(<proc> ^name waiting-answer ^topic greeting ^object <obj>)
-->
	(<f> ^text |Looks like he ran away. What a shy person.|)
}
