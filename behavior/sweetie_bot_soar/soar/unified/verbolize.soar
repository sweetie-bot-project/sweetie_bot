# VERBOLIZE SUBSTATE
#  
# DESCRIPTION
#
# Say phrase with animation.
#
# DECISION OPERATOR
#
# (<op> ^name @name@ ^substate verbolize ^process-args <args>) 
#   (<args> ^object <obj> ^topic <topic> ^text <text> ^animation-tag <tag> [^sound <sound-file>] [^scene <scene>] [^character <character>])
#
# Say person <obj> about <topic> with <text> or <sound-file> and using animation tagged with <tag>.
#
# Set ^animation-tag to none to skip animation. Multiply ^animation-tag may be specified. 
# Substate consider only animation tagged with talk tag. Animation preferences are following:
#  * two tags is matched (most preferable)
#  * one tag is matched
#  * animation have ^teg general
#
# State publishes SpeechVisualization message with attributes text, sound, character, scene.
#
#
# PROCESS RESULT STATUSES
#
# * succeed --- text action succced and animation outcome is received
#
# SIDE EFFECTS
#
# Substates:
#
# tect-action, flexbe, look-at
#
# Process:
#
# * (^name talk-said ^object <object> ^topic <topic>) --- said to <object> about <topic>.
#

# DECISION RULES

# PERFORM TEXT ACTION

# text action: play sound file  
# if text has not been verbolzed and is not being verbolized and we have not failed with sound verbolization befor
# synthesis has lower priority then predefined sounds
sp {verbolize*propose*text-action-sound
	(state <s> ^substate verbolize -^performing-text-action ^args <args> ^process <proc>)
	(<args> ^sound <file>)
	(<proc> -^text-action-status -^text-action-sound-failed)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name performing-text-action ^substate text-action ^process-args <a> ^priority 27)
    (<a> ^type |voice/play_wav| ^command <file> ^result <r-succeed> <r-failed>)
	(<r-succeed> ^status succeed ^parent <proc> ^attribute text-action-status ^link-value succeed)
	(<r-succeed> ^print (concat |TALK: text action play file: | <file> (crlf)))
	(<r-failed> ^status failed ^parent <proc> ^attribute text-action-sound-failed ^link-value yes)
}

# text action: speech synthesis
# propose it if text has not been verbolized and is not being verbolized
# synthesis has lower priority then predefined sounds
sp {verbolize*propose*text-action-text
	(state <s> ^substate verbolize -^performing-text-action ^args <args> ^process <proc>)
	(<args> ^text <text>)
	(<proc> -^text-action-status)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name performing-text-action ^substate text-action ^process-args <a> ^priority 25)
    (<a> ^type |voice/say| ^command <text> ^result <r>)
	(<r> ^status succeed ^parent <proc> ^attribute text-action-status ^link-value succeed)
	(<r> ^print (concat |TALK: text action say: | <text> (crlf)))
}

# SEND VISUALIZATION MESSAGE

# speech visualization: construct SpeechVisualization command
sp {verbolize*propose*speech-visualization
	(state <s> ^substate verbolize ^args <args> ^process <proc>)
	(<args> ^text <text> ^object <obj> ^topic <topic>)
	(<proc> -^text-is-visualized)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name visualize-speech ^type automatic ^priority 23)
	(<op> ^parent <proc> ^attribute text-is-visualized ^link-value yes)
	(<op> ^add-output-command speech-visualization ^output-command-args <cmd>)
	(<cmd> ^text <text> ^automatic-remove yes)
}

# copy scene and character attributes
sp {verbolize*elaborate*speech-visualization-scene-character
	(state <s> ^substate verbolize ^operator <op> ^args <args>)
	(<op> ^name visualize-speech ^output-command-args <cmd>)
	(<args> ^{<<|scene| |character|>> <attr>} <value>)
-->
	(<cmd> ^<attr> <value>)
}

# copy animation-tag attributes
sp {verbolize*elaborate*speech-visualization-tags
	(state <s> ^substate verbolize ^operator <op> ^args <args>)
	(<op> ^name visualize-speech ^output-command-args <cmd>)
	(<args> ^animation-tag <value>)
-->
	(<cmd> ^tag <value>)
}

# SELECT AND PERFORM ANIMATION

# select general animations
sp {verbolize*elaborate*animation-general
	(state <s> ^substate verbolize ^mem.joint-trajectory <anim>)
	(<anim> ^tag talk general)
-->
	(<s> ^animation <anim>)
}

# select all matching animations
sp {verbolize*elaborate*animation-matching-tag
	(state <s> ^substate verbolize ^args.animation-tag <tag> ^mem.joint-trajectory <anim>)
	(<anim> ^tag talk <tag>)
-->
	(<s> ^animation <anim>)
}

# animation-tag none disables animation: succeed it immediately
sp {verbolize*propose*ditch-animation
	(state <s> ^substate verbolize -^performing-animation ^args.animation-tag none ^process <proc>)
	(<proc> -^animation-status)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name ditch-animation ^type automatic ^priority 40 ^parent <proc> ^attribute animation-status ^link-value succeed)
}

# propose animations, agument process predicate if animation succeed
sp {verbolize*propose*animation
	(state <s> ^substate verbolize -^performing-animation ^animation <anim> ^process <proc>)
	(<proc> -^animation-status)
	(<anim> ^name <animation-name>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name performing-animation ^substate flexbe ^animation <anim> ^process-args <a>)
    (<a> ^behavior ExecuteJointTrajectory ^joint_trajectory <animation-name> ^result <r>)
	(<r> ^status succeed invalid_pose failed ^parent <proc> ^attribute animation-status ^link-value succeed)
}

# elaborate is_relative ExecuteJointTrajectory parameter
sp {verbolize*elaborate*say-execute-animation-is-relative
	(state <s> ^substate verbolize ^operator <op> +)
	(<op> ^name performing-animation ^process-args <args> ^animation.relative) 
-->
	(<args> ^is_relative True)
}


# WAITING 
# TODO: correct implementation of parallel processe: both substates should be placed in stack

# look at speaker if animation is finished
sp {verbolize*propose*looking-at-speaker
	(state <s> ^substate verbolize ^process <proc>)
	(<proc> ^animation-status succeed ^object <obj>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name looking-at-speaker ^substate look-at ^process-args <a> ^priority 20)
	(<a> ^object <obj> ^timeout 5.0 ^delay 1.0)
}

# waiting is worst choice
sp {verbolize*elaborate*waiting
	(state <s> ^substate verbolize)
-->
	(<s> ^operator <op> +, <)
	(<op> ^name waiting ^substate wait)
}

# PREFERENCES 
#
# Utility:
# ditch aniamtion --- 40
# perform aniamtion 
#     two matching tags --- 32
#     one matching tags --- 31
#     no  matching tags --- 30
# perform text action --- 25
# looking at speaker --- 20
# waiting --- worst
#

# calculate animation priority: two tags
sp {verbolize*elaborate*animation-two-tag-priority
	(state <s> ^substate verbolize ^operator <op> + ^args <args>)
	(<args> ^animation-tag <tag1> ^animation-tag {<> <tag1> <tag2>})
	(<op> ^name performing-animation ^animation <anim>)
	(<anim> ^tag <tag1> ^tag <tag2>)
-->
	(<op> ^priority 32)
}

# calculate animation priority: one tag
sp {verbolize*elaborate*animation-one-tag-priority
	(state <s> ^substate verbolize ^operator <op> + ^args.animation-tag <tag>)
	(<op> ^name performing-animation ^animation.tag <tag> -^priority 32)
-->
	(<op> ^priority 31)
}

# calculate animation priority: no tags
sp {verbolize*elaborate*animation-no-tag-priority
	(state <s> ^substate verbolize ^operator <op> +)
	(<op> ^name performing-animation -^priority 32 -^priority 31)
-->
	(<op> ^priority 30)
}

# prefer operator with higher priority
sp {verbolize*prefer*priority
	(state <s> ^substate verbolize ^operator <op1> + ^operator {<> <op1> <op2>} ^args <args>)
	(<op1> ^priority <value>)
	(<op2> ^priority {< <value>})
-->
	(<s> ^operator <op1> > <op2>)
}

# COGNITION OPERTORS

# assume verbolize process finished if animation and text-action succeed
sp {verbolize*propose*terminate-verbolize
	(state <s> ^bottom-state 1 ^beliefs <b>)
	(<b> ^process <proc>)
	(<proc> ^substate verbolize ^text-action-status succeed ^animation-status ^object <obj> ^topic <topic>)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name terminate-verbolize ^type cognition automatic ^finish-process <proc> ^finish-status succeed ^add-event <ev-new>)
	(<ev-new> ^name talk-said ^object <obj> ^topic <topic> ^memorization-time 3600.0)
}

#sp {verbolize*propose*on-text-action-succeed
#	(state <s> ^bottom-state 1 ^beliefs <b>)
#	(<b> ^event <ev>)
#	(<ev> ^substate text-action ^status succeed ^process.text <text>)
#	(<b> ^process <proc>)
#	(<proc> ^substate verbolize ^text <text>)
#-->
#	(<s> ^operator <op> +, =)
#	(<op> ^name on-text-action-succeed ^type cognition automatic ^parent <proc> ^attribute text-action-status ^link-value succeed)
#}

