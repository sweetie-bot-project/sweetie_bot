# GENERAL BEHAVIORS
#
# Form stack of substate corresponding for different drives based in their importance.
#
# DESCRIPTION
#
# Drive description
#
#    (<s> ^mem <m>)
#        (<m> ^drive <d1> <d2>)
#             (<d1> drive1 ^importance 1)
#             (<d2> drive2 ^importance 2)
#
# will form the following substate stack
#
#    (S1 ^name drive2 ^substate general ^superstate nil)
#        (S2 ^name drive1 ^substate general ^superstate S1)
#
# Change of drive importance results in stack regeneration.
# Rules which implements drives should be placed in coresponding substate. 
# 

# ELABORATIONS

# derive drives which should be excluded beacuse they are considered in superstate
sp {general*elaborate*exclude-drives
	(state <s> ^substate general ^superstate.{<<|drive-active| |drive-exclude|>>} <d>)
-->
	(<s> ^drive-exclude <d>)
}

# select drives to be considered as active
sp {general*elaborate*possible-drive
	(state <s> ^substate general ^mem.drive <d> -^drive-exclude <d>)
-->
	(<s> ^drive-possible <d>)
}

# select the most preferable drive: add substate name and selected drive
sp {general*elaborate*active-drive
	(state <s> ^substate general ^drive-possible <d1>)
	(<d1> ^name <name> ^importance <i1>)
	-{(<s> ^drive-possible <d2>) 
	  (<d2> ^importance {> <i1>})}
-->
	(<s> ^drive-active <d1>)
	(<s> ^name <name>)
}

# attach existing top-level process 
sp {general*elaborate*attach-top-level-process
	(state <s> ^substate general ^name <name> ^beliefs.process <proc>)
	(<proc> ^name <name> ^substate general)
-->
	(<s> ^process <proc> ^process-status active)
}

# create top-level process
sp {general*elaborate*create-top-level-process
	(state <s> ^substate general ^name <name> -^process-status active)
-->
	(<s> ^process <proc> ^process-status pending)
	(<proc> ^name <name> ^substate general)
}

#
# DECISION RULES
#

# the worst possible choice: select next drive
sp {general*propose*select-next-drive
	(state <s> ^name ^substate general)
-->
	(<s> ^operator <op> +, <, =)
	(<op> ^name top-level-goal)
}

# the worst possible choice: wait if no drives left
sp {general*propose*wait
	(state <s> -^name ^substate general)
-->
	(<s> ^operator <op> +, <)
	(<op> ^name waiting ^substate wait)
}

