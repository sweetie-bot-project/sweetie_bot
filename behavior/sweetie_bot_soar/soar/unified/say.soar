# SAY SUBSTATE
#  
# DESCRIPTION
#
# Focus attention on speaker and say phrase, which can be accomplished with animation.
#
# DECISION OPERATOR
#
# (<op> ^name @name@ ^substate say ^object <obj> ^topic <topic> ^text <text> ^animation-tag <tag>)
#
# Say person about <topic> with <text> and using animation tagged with <tag>.
#
# RESULT STATUSES
#
# * succeed --- desired phrase is said to <object>.
# * failed --- was unable to focus on <object>.
#
# SIDE EFFECTS
#
# Events:
#
# * (^name talk-said ^object <object> ^topic <topic>) --- said to <object> about <topic>.
# * (^name talk-failed ^object <object> ^topic <topic>) --- unable to focus attention on <object>.
#
# Decisions:
#
# * (^name say ^substate say ^object <obj> ^topic <topic> ^deadline-time 2.0) --- gaze is focused on object so we can say <text>.
#

# ELABORATIONS

# match decision to the sustate
sp {say*elaborate*match-decision
	(state <s> ^substate say ^beliefs.decision <d> ^superstate.operator <sop>)
	(<d> ^name say ^substate say ^object <obj> ^topic <topic>)
	(<sop> ^object <obj> ^topic <topic>)
-->
	(<s> ^decision <d>)
}

# DECISION RULES

# enshure that object is in center of view before asking
sp {say*propose*look-at
	(state <s> ^substate say -^decision ^superstate.operator <sop>)
	(<sop> ^object <obj> ^topic <topic>)
-->
	(<s> ^operator <op> +, <)
	(<op> ^name say-look-at ^substate look-at ^object <obj> ^timeout 10.0 ^delay 2.0 ^result <r-failed>)
	(<r-failed> ^status failed ^add-event <ev>)
	(<ev> ^name talk-failed ^object <obj> ^topic <topic> ^memorization-time 3600.0)
}

# choice animation with tag 'talk'
sp {say*propose*select-animation
	(state <s> ^substate say -^decision ^beliefs.predicate <pred> ^superstate.operator <sop> ^mem.joint-trajectory <anim>)
	(<sop> ^object <obj> ^topic <topic>)
	(<pred> ^name looking-at ^object <obj>)
	(<anim> ^tag talk)
-->
	(<s> ^operator <op> +, =)
	(<op> ^name select-animation ^type automatic ^animation <anim> ^add-decision <d>)
	(<d> ^name say ^substate say ^object <obj> ^topic <topic> ^animation <anim> ^deadline-time 30.0)
}


# if desicion to ask is made execute flexbe command
sp {say*propose*say-execute
	(state <s> ^substate say ^decision <d> ^superstate.operator <sop>)
	(<d> ^object <obj> ^topic <topic> ^animation <anim>)
	(<anim> ^name <animation-name>)
	(<sop> ^text <text>)
-->
	(<s> ^operator <op> +)
	(<op> ^name say-execute ^substate flexbe ^behavior ExecuteJointTrajectoryAndSay ^param <param> ^animation <anim> ^result-inherit succeed ^result <res> ^result <res-succeed>)
    (<param> ^text <text> ^joint_trajectory <animation-name> ^text_delay 0.2)
	(<res> ^status failure invalid_pose ^remove-decision <d>)
	(<res-succeed> ^status succeed ^add-event <ev> ^remove-decision <d>)
	(<ev> ^name talk-said ^object <obj> ^topic <topic> ^memorization-time 3600.0)
}

# elaborate is_relative ExecuteJointTrajectoryAndSay paramter
sp {say*elaborate*say-execute-animation-is-relative
	(state <s> ^substate say ^operator <op> +)
	(<op> ^name say-execute ^param <param> ^animation.relative) 
-->
	(<param> ^is_relative True)
}
 
# PREFERENCES 

# prefer animation with tag matching supplied as substate parameter
sp {say*prefer*select-animation-tag
	(state <s> ^substate say ^operator <op1> + ^operator {<> <op1> <op2>} ^superstate.operator.animation-tag <tag>)
	(<op1> ^name select-animation ^animation.tag <tag>)
	(<op2> ^name select-animation ^animation <anim2>)
	(<anim2> -^tag <tag>)
-->
	(<s> ^operator <op1> > <op2>)
}

# reject animations which are not tagged as general 
sp {say*prefer*select-animation-general-tag
	(state <s> ^substate say ^operator <op> + ^superstate.operator.animation-tag <tag>)
	(<op> ^name select-animation ^animation <anim2>)
	(<anim2> -^tag <tag> -^tag general)
-->
	(<s> ^operator <op> ~)
}
