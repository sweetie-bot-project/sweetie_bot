# STEP-SEQUENCE GOAL

# Execute the sequence of step-sequences or walk along directed graph of step sequences
# Goal have three state: move (step sequence execution), select-next-move (selection next step secuence) and complete.
# Next step sequence selected via next attribute of current step sequence.
# 
# goal example (executes move_fwd_40 two times): 
#
# (G1 ^name step-sequence ^step-sequence Q1 ^state move)
#   (Q1 ^name move_fwd_40 ^next Q2)
#   (Q2 ^name move_fwd_40)
#

# propose execution (move)
sp {propose*goal*step-sequence*execution
	(state <s> ^mem.goal <g> ^io.output-link <ol>)
	(<g> ^name step-sequence ^step-sequence.name <step-seq-name> ^state move)
	(<ol> - ^flexbe)
-->
	(<s> ^operator <o> + =)
   	(<o> ^name flexbe <o> ^goal <g>)
   	(<o> ^behavior ExecuteStepSequence ^param <p>)
	(<p> ^action_name <step-seq-name>)
}

# completion (move)
sp {apply*flexbe*cmd*completed*hook*goal*step-sequence*
	(state <s> ^operator <op>)
   	(<op> ^name flexbe-completed ^cmd <cmd>)
	(<cmd> ^status completed ^outcome finished ^operator.goal <g>)
	(<g> ^name step-sequence)
-->
	(<g> ^state move -)
	(<g> ^state select-next-move)
}

# propose (select-next)
sp {propose*goal*step-sequence*select-next
	(state <s> ^mem.goal <g>)
	(<g> ^name step-sequence ^step-sequence.next <next-step-seq> ^state select-next-move)
-->
	(<s> ^operator <o> + >)
   	(<o> ^name goal-step-sequence-select-next <o> ^goal <g> ^next <next-step-seq>)
}

# apply (select-next)
sp {apply*flexbe*step-sequence*select-next
	(state <s> ^operator <op>)
   	(<op> ^name goal-step-sequence-select-next ^goal <g> ^next <next-step-seq>)
	(<g> ^step-sequence <step-seq>)
-->
	(<g> ^state select-next-move -)
	(<g> ^state move)
	(<g> ^step-sequence <step-seq> -)
	(<g> ^step-sequence <next-step-seq>)
	(write |Goal updated: next step-sequence|)
}

# propose (complete)
sp {propose*goal*step-sequence*complete
	(state <s> ^mem.goal <g>)
	(<g> ^name step-sequence ^step-sequence <step-seq> ^state select-next-move)
	(<step-seq> - ^next)
-->
	(<s> ^operator <o> + >)
   	(<o> ^name goal-step-sequence-complete <o> ^goal <g>)
}

# apply (complete)
sp {apply*flexbe*step-sequence*complete
	(state <s> ^operator <op> ^mem <m>)
   	(<op> ^name goal-step-sequence-complete ^goal <g>)
	(<g> ^step-sequence <step-seq>)
-->
	(<g> ^state select-next-move -)
	(<g> ^state completed)
	(<m> ^goal <g> -)
	(<m> ^completed-goal <g>)
	(write |Goal completed: step-sequence|)
}
