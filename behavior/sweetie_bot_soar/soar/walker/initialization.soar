# INITIALIZATION

# FIRST STAGE
#
# drives and mem
#

sp {propose*initilization
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <o> +)
   (<o> ^name initialization-first-stage)
}

sp {apply*initialization-first-stage*name
   (state <s> ^operator <op>)
   (<op> ^name initialization-first-stage)
-->
	(<s> ^name initialization-first-stage)
}

sp {apply*initialization*drive*mem
   (state <s> ^operator <op>)
   (<op> ^name initialization-first-stage)
-->
	(<s> ^drive <d1> <d2> <d3>)
	(<d1> ^name movement ^activation 0 ^importance 2)
	(<d2> ^name obey ^activation 0 ^importance 8)
	(<d3> ^name security ^activation 0 ^importance 10)
	(<s> ^mem <m>)
}

# SECOND STAGE
#
# put things into memory
#

sp {propose*initilization-second-stage
   (state <s> ^name initialization-first-stage)
-->
   (<s> ^operator <o> +)
   (<o> ^name initialization-second-stage)
}

sp {apply*initialization*mem*compound*action
   (state <s> ^operator <op> ^mem <m>)
   (<op> ^name initialization-second-stage)
-->
	(<m> ^compound-action <c1> <c2> <c3> <c4> <c5> <c6> <c7> <c8> <c9> <c10> <c11> <c12> <c13> <c14> <c15> <c16> <c17> <c18> <c19>)
	(<c1> ^name brohoof_acquitance_procedure ^tag greet)
	(<c2> ^name prance_maximum_fun ^tag alone ^tag talk)
	(<c3> ^name menace_prepare_to_die ^tag anger)
	(<c4> ^name brohoof_do_you_want ^tag greet)
	(<c5> ^name hoof_stamp_do_you_want_attention ^tag anger)
	(<c6> ^name point_control_you ^tag anger)
	(<c7> ^name brohoof ^tag greet ^tag alone )
	(<c8> ^name introduce_herself_friendship ^tag greet)
	(<c9> ^name point_do_not_touch ^tag anger)
	(<c10> ^name greeting_friendship_programs ^tag greet)
	(<c11> ^name menace ^tag anger ^tag alone)
	(<c12> ^name head_ask_about_sister ^tag talk)
	(<c13> ^name sing_song1 ^tag talk ^tag alone )
	(<c14> ^name sing_song2 ^tag talk ^tag alone )
	(<c15> ^name complex_hybrid_state_machines ^tag talk ^tag alone)
	(<c16> ^name seizure_cutie_mark_acquisition ^tag talk  ^tag alone)
	(<c17> ^name applause_you_must_be_upgraded ^tag anger )
	(<c18> ^name point_i_have_remembered_you ^tag anger )
	(<c19> ^name head_shake_dizzy ^tag alone )
}


sp {apply*initialization*mem*step-sequnces
	(state <s> ^operator <op> ^mem <m> ^drive <d>)
	(<op> ^name initialization-second-stage)
	(<d> ^name movement)
-->
	(<m> ^step-sequence <s1> <s2> <s3> <s4> <s5> <s6> <s7> <s8> <s9> <s10> <s11> <s12> <s13>)
	(<s1> ^name turn_right_20_20_45 ^next <s2> ^enter-point yes)
	(<s2> ^name turn_left_45 ^next <s3>)
	(<s3> ^name turn_left_20_20_45 ^next <s4>)
	(<s4> ^name turn_right_45 ^next <s5>)
	(<s5> ^name walk_fwd_40 ^next <s6>)
	(<s6> ^name turn_left_20_20_90 ^next <s7>)
	(<s7> ^name turn_left_90 ^next <s8>)
	(<s8> ^name walk_fwd_40 ^next <s9>)
	(<s9> ^name turn_right_45 ^next <s10>)
	(<s10> ^name turn_right_45 ^next <s11>)
	(<s11> ^name backslide_right_10_00_45 ^next <s12>)
	(<s12> ^name walk_back_20 ^next <s13>)
	(<s13> ^name backslide_right_20_20_90 ^next <s1>)
}

#
# FINISH INITIALIZATION
#
# set state name

sp {apply*initialization-second-stage*name
   (state <s> ^operator <op> ^name <name>)
   (<op> ^name initialization-second-stage)
-->
	(<s> ^name walker)
	(<s> ^name <name> -)
	(write |Initialization finished.|)
}
